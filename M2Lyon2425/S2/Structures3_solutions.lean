import Mathlib.Data.Real.Basic
import Mathlib.Topology.MetricSpace.Cauchy
import Mathlib.Topology.Instances.Real
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.Topology.UniformSpace.Basic
import Mathlib.Data.Complex.Exponential
import Mathlib.Topology.MetricSpace.Basic


noncomputable section

open Classical

section LocalInstances

open scoped Filter Uniformity

#print UniformSpace
-- One constructor and four fields

example : instUniformSpaceNat = âŠ¥ := rfl

example : (uniformity â„•) = (ğ“Ÿ idRel) := rfl -- this is the "trivial" or "discrete" uniformity.

#synth UniformSpace â„• -- instUniformSpaceNat
attribute [- instance] instUniformSpaceNat --this is local, it only applies to the current section

#synth UniformSpace â„• -- failed to synthesize

def PSM_Nat : PseudoMetricSpace â„• where --use the ğŸ’¡-action
  dist := fun n m â†¦ |2 ^ (- n : â„¤) - 2 ^ (- m : â„¤)|
  dist_self := by simp only [zpow_neg, zpow_natCast, sub_self, abs_zero, implies_true]
  dist_comm := fun _ _ â†¦ abs_sub_comm _ _
  dist_triangle := fun _ _ _ â†¦ abs_sub_le .. -- a word about `..`

attribute [instance] PSM_Nat

-- local instance : PseudoMetricSpace â„• where
--   dist := fun n m â†¦ |2 ^ (- n : â„¤) - 2 ^ (- m : â„¤)|
--   dist_self := by simp only [zpow_neg, zpow_natCast, sub_self, abs_zero, implies_true]
--   dist_comm := fun _ _ â†¦ abs_sub_comm ..
--   dist_triangle := fun _ _ _ â†¦ abs_sub_le _ _ _

#synth UniformSpace â„• -- PseudoMetricSpace.toUniformSpace

/-! This is actually true! See `Counterexamples/DiscreteTopologyNonDiscreteUniformity.lean`-/
lemma idIsCauchy : CauchySeq (id : â„• â†’ â„•) := by sorry

example : CauchySeq (id : â„• â†’ â„•) := idIsCauchy

end LocalInstances

-- This does not work, since we have quit the `LocalInstance` section
example : CauchySeq (id : â„• â†’ â„•) := idIsCauchy


-- `âŒ˜`


noncomputable section Synonyms

notation "ğ’©" => â„•
#check (37 : ğ’©)

abbrev AbbNat := â„•
#check (37 : AbbNat)

def DefNat := â„•
#check (37 : DefNat)

def DefSucc (a : â„¤) := a + 1 --definition as in Mathlib
abbrev AbbSucc (a : â„¤) := a + 1 --

example (a : â„¤) : DefSucc (DefSucc a) = a + 2 := by simp only [add_assoc, Int.reduceAdd]

example (a : â„¤) : AbbSucc (AbbSucc a) = a + 2 := by simp only [add_assoc, Int.reduceAdd]


-- `âŒ˜`


abbrev ğ“¡ := â„ --type ğ“¡ with \MCR
#synth TopologicalSpace ğ“¡

attribute [-instance] UniformSpace.toTopologicalSpace
#synth TopologicalSpace â„

instance TopSpaceğ“¡ : TopologicalSpace ğ“¡ := âŠ¥
#synth TopologicalSpace ğ“¡
#synth TopologicalSpace â„

example : Continuous (fun x : â„ â†¦ if x < 0 then (0 : â„) else 1) := by
  apply continuous_bot
/-`continuous_bot` is the statement saying that every function leaving from a discrete space
is automatically continuous. -/


def â„› := â„ --type â„› with \McR

#synth TopologicalSpace â„›
#synth Field â„›

instance : TopologicalSpace â„› := âŠ¥

instance : Field â„› := inferInstanceAs (Field â„)

#synth CommRing â„›
#synth CommRing ğ“¡

instance : LT â„› := inferInstanceAs <| LT â„ -- a word about `<|`

lemma ContJump : Continuous (fun x : â„› â†¦ if x < 0 then (0 : â„›) else 1) := by
  apply continuous_bot

lemma ContJump' : Continuous (fun x : ğ“¡ â†¦ if x < 0 then (0 : ğ“¡) else 1) := by
  apply continuous_bot

-- This might be a problem!
lemma ContJump'' : Continuous (fun x : â„ â†¦ if x < 0 then (0 : â„) else 1) := by
  apply continuous_bot

end Synonyms

-- Even leaving the `Synonyms` section, the following still works.
example : Continuous (fun x : â„› Ã— â„› â†¦ if x.1 < 0 then (0 : â„›) else 1) := by
  exact ContJump.comp continuous_fst


-- `âŒ˜`


section Structures


structure OneNat where
  fst : â„•

structure TwoNat where
  pair ::
  fst : â„•
  snd : â„•

whatsnew in
@[ext]
structure Couple where
  left : â„•
  right : â„•

whatsnew in
@[class]
structure OrderedPairs where
  fst : â„•
  snd : â„•
  order : fst â‰¤ snd -- this field depends upon the previous two.

#check OneNat.mk
#check TwoNat.mk
#check TwoNat.pair
#check OrderedPairs.mk
#check order
#check OrderedPairs.order


structure TwoTerms (Î± : Type) where
  fst : Î±
  snd : Î±

structure Mess (Î± Î² Î³ : Type) [Zero Î±] [TopologicalSpace Î²] [UniformSpace Î³] :=--`where` or `:=`
  a : Î± := 0
  f : Î± â†’ Î² â†’ Î³ â†’ Î³
  cont : Continuous (f a)

#print Mess


-- `âŒ˜`


attribute [-instance] TopSpaceğ“¡
-- ## Constructing terms

example : TwoNat := sorry --farlo con `:=`, poi aggiungere _, lampadina, etc...

open Real

-- What happens if we have a default value?
def x1 : Mess â„• â„ â„ where
  f := fun n x y â†¦ n + x + y
  cont := by
    simp only [CharP.cast_eq_zero, zero_add]
    apply continuous_pi
    exact fun i â†¦ continuous_add_right i

def x2 : Mess â„• â„• â„• where
  a := 37
  f := fun n x y â†¦ n + x + y
  cont := by
    apply continuous_bot

example (x : TwoNat) : Couple where
  left := x.fst
  right := x.snd

-- Same construction, different syntax
example (x : TwoNat) : Couple := {left := x.fst, right := x.snd}

example : Couple := âŸ¨3, 2âŸ©

example (x : OneNat) : Couple :=
  {left := x.1, right := x.1}


-- `âŒ˜`


-- This forgets the label and takes it back.
example (x : OneNat) : TwoNat :=
  {x with snd := x.1}

-- another syntax
example (x : OneNat) : TwoNat where
  __ := x
  snd := x.1

example (x : TwoNat) : OneNat := {x with} --without the `with` the extra-field is not thrown away

example (x : TwoNat) : OneNat where
 __ := x

example (x : TwoNat) : Couple := x

example (x : OneNat) : Couple := sorry
  -- {x with left := x.1} fields missing: 'right'
--so, it does not "populate missing fields with the first available type-correct term: labels matter"

example (x : OneNat) : â„• := sorry--{x with}

structure Mix where
  fst : â„•
  right : â„•

#check Mix.mk

def mix1 (x : TwoNat) (y : Couple) : Mix :=
  {x, y with}
/- remember that `x := {x.fst, x.snd}`, `y = {y.left, y.right}`
  and `Mix.mk` takes a `fst : â„•` and `right : â„•`: se we need to throw away `x.snd` and `y.left`-/

def mix1' (x : TwoNat) (y : Couple) : Mix where
  __ := x
  __ := y

-- the order does not really matter, it "destructs and reconstructs".
def mix2 (x : TwoNat) (y : Couple) : Mix :=
  {y, x with}


example : mix1 = mix1' := rfl

example : mix1 = mix2 := rfl


-- An example with structures having three terms.
structure Mix' where
  snd : â„•
  left : â„•

structure ThreeNat where
  fst : â„•
  snd : â„•
  thrd : â„•

structure Mixâ‚ƒ where
  right : â„•
  left : â„•
  thrd : â„•

/- `x := {x.fst, x.right}`, `y := {y.snd, y.left}`, `z := {z.fst, z.snd, z.thrd}` and `Mix.mk` takes
a `fst : â„•` and a `right : â„•`: we need to throw away `x.left`, `y.left`, `z.snd` and `z.thrd`-/
example (x : Mix) (y : Mix') (z : ThreeNat) : Mixâ‚ƒ :=
  {x, y, z with}

-- A final example with a `Prop`-valued field:

def fâ‚ : Mess â„• â„• â„• where
  f := fun a b â†¦ a + b
  cont := {isOpen_preimage := fun _ _ â†¦ trivial}
  -- cont := âŸ¨fun _ _ â†¦ trivialâŸ©

def fâ‚‚ : Mess â„• â„• â„• where
  f := fun a b â†¦ a + b
  cont := continuous_of_discreteTopology

-- `Prop`-valued fields disappear by proof irrelevance
example : fâ‚ = fâ‚‚ := rfl


-- `âŒ˜`


-- ## Extends


structure TwoNatExt extends OneNat where
  snd : â„•

structure OneNatOneInt where
  fst : â„•
  snd : â„¤

structure Blob extends OneNatOneInt, OneNat
structure Blob' extends OneNatOneInt, TwoNat


/- Under the hood, Lean destructs all these terms and reconstructs them "in the right order" --- but
keeping labels. -/

def TwoExtToCouple : TwoNatExt â†’ Couple := by --fun x â†¦ {left := x.1, right := x.2} -- error! why?
  rintro âŸ¨x, yâŸ© -- by def, `TwoNatExt` extends `OneNat`, so `x : OneNat`. So,
  exact {left := x.1, right := y}

def TwoNatToCouple : TwoNat â†’ Couple := fun x â†¦ {left := x.1, right := x.2}

/- And if there are duplicates? Remember that
  `structure Mix where`
      `fst : â„•`
      `right : â„•`     -/

structure ThreeNatExt extends TwoNat, Mix

#print ThreeNatExt

/- Overlapping fields are not duplicated; moreover, whenever possible, fields will coincide with
constructors of the parent structure; in case of overlapping fields, things are destructured. -/


def TwoNatToExt : TwoNat â†’ TwoNatExt := fun x â†¦ {x with}

/- `with` is able to
1. Destruct `x` into `x.fst` and get a `OneNat`
2. Out of the `OneNat` reuire another `â„•` to define a `TwoNatExt`
3. Destruct `x` into `x.snd` and get the missing field. -/

example (x : TwoNat) : TwoNatToExt x = âŸ¨âŸ¨x.fstâŸ©, x.sndâŸ© := rfl


/- Remember
    `mix1 (x : TwoNat) (y : Couple) : Mix := {x, y with}` and
    `mix2 (x : TwoNat) (y : Couple) : Mix := {y, x with}` -/

def mix3 (x : TwoNatExt) (y : Couple) : Mix :=
  {x, y with}

example (x : TwoNat) (y : Couple) : mix1 x y = mix3 (TwoNatToExt x) y := rfl


/- Remember that `ThreeNatExt extends TwoNat, Mix` and
  `structure Mix where`
        `fst : â„•`
        `right : â„•`     -/

def Mâ‚ : Mix := {fst := 17, right := 11}
def two : TwoNat := {fst := 1, snd := 2}

def threeâ‚ : ThreeNatExt := {Mâ‚, two with}
def threeâ‚' : ThreeNatExt := {two, Mâ‚ with}

example : threeâ‚.fst = 17 := by rfl
example : threeâ‚'.fst = 1 := by rfl

/- So in reality Lean is first using the first variable (`M` or `two`), possibly throwing away
useless stuff, and then using what follows to complete them -/

example : threeâ‚ = threeâ‚' := sorry--rfl -- (they're even different, not simply not `defeq`..._
--  indeed, `threeâ‚={fst = 17, snd = 2, right = 11}` while `threeâ‚'={fst = 1, snd = 2, right = 11}`-/


def Mâ‚‚ : Mix := {fst := 13, right := 11}
def threeâ‚‚' : ThreeNatExt := {two, Mâ‚‚ with}

example : threeâ‚‚'.fst = 1 := rfl
example : threeâ‚' = threeâ‚‚' := rfl -- one uses `Mâ‚`, and the other uses `Mâ‚‚`.
/- both are `{fst = 1, snd = 2, right = 11}` (the field `left` has been discharged) . -/

structure TwoNatExtLeft extends TwoNat where
  left : â„•

example (x : ThreeNat) (y : Couple) : TwoNatExtLeft := sorry


-- `âŒ˜`


/- ### In True Math
Remember the piece of code-/

-- We can now go back to what we saw last week: remember that we defined
class AddMonoidBad (M : Type) extends Add M, AddZeroClass M

instance : AddMonoidBad â„• where --created using `:=` â†’ `_` â†’ ğŸ’¡
  add := Nat.add
  zero := Nat.zero
  zero_add := Nat.zero_add
  add_zero := Nat.add_zero

instance : AddMonoidBad â„• := âŸ¨Nat.zero_add, Nat.add_zeroâŸ©
-- instance : AddMonoidBad â„• := âŸ¨Nat.add_zero, Nat.zero_addâŸ© -- order matters!

instance : AddMonoidBad â„• := {Nat.instAddMonoid with}

instance : AddMonoidBad â„• where
  __ := Nat.instAddMonoid

end Structures

section Exercises
attribute [-instance] TopSpaceğ“¡

/- ## Exercise 1
Define the class of metric spaces (but call them `SpaceWithMetric` to avoid conflict with the
existing library) as defined in https://en.wikipedia.org/wiki/Metric_space#Definition, and deduce
an instance of `TopologicalSpace` on every `SpaceWithMetric`.

Explain why this is the *wrong* choice, on an explicit example, and fix this.
-/

@[ext]
class SpaceWithMetric (X : Type) where
  d : X â†’ X â†’ â„
  dist_eq_zero (x : X) : d x x = 0
  dist_pos (x y : X) : x â‰  y â†’ 0 < d x y
  symm (x y : X) : d x y = d y x
  triangle (x y z : X) : d x z â‰¤ d x y + d y z

@[simp]
instance (X : Type) [SpaceWithMetric X] : TopologicalSpace X where
  IsOpen := by
    intro S
    exact âˆ€ x âˆˆ S, âˆƒ Ï : â„, 0 < Ï âˆ§ {y | SpaceWithMetric.d x y < Ï} âŠ† S
  isOpen_univ := by
    by_cases hX : Nonempty X
    Â· rintro x -
      use 1
      simp
    Â· tauto
  isOpen_inter := by
    intro S T hS hT x âŸ¨hxS, hxTâŸ©
    let ÏS := (hS x hxS).choose
    let ÏT := (hT x hxT).choose
    use min ÏS ÏT
    constructor
    Â· apply lt_min
      exact (hS x hxS).choose_spec.1
      exact (hT x hxT).choose_spec.1
    apply Set.subset_inter
    Â· apply subset_trans _ (hS x hxS).choose_spec.2
      intro _ hy
      simp only [lt_min_iff, Set.mem_setOf_eq] at hy
      exact hy.1
    Â· apply subset_trans _ (hT x hxT).choose_spec.2
      intro _ hy
      simp only [lt_min_iff, Set.mem_setOf_eq] at hy
      exact hy.2
  isOpen_sUnion := by
    intro Î© hÎ© x âŸ¨T, hT, hxâŸ©
    use (hÎ© T hT x hx).choose
    constructor
    Â· exact (hÎ© T hT x hx).choose_spec.1
    apply subset_trans (hÎ© T hT x hx).choose_spec.2
    exact Set.subset_sUnion_of_subset Î© T (by rfl) hT

@[simp]
instance : SpaceWithMetric â„ where
  d := fun x y â†¦ dist x y
  dist_eq_zero := by simp
  dist_pos := fun x y â†¦ (dist_pos).mpr
  symm := dist_comm
  triangle := by exact fun x y z â†¦ dist_triangle x y z


#synth TopologicalSpace â„

example : Continuous (fun x : â„ â†¦ x + 1 ) := by
  sorry
  -- exact continuous_add_right 1

example : instTopologicalSpaceOfSpaceWithMetric â„ = UniformSpace.toTopologicalSpace := by
  ext U
  rw [Metric.isOpen_iff, IsOpen]
  simp only [instTopologicalSpaceOfSpaceWithMetric, dist_comm, Metric.ball, instSpaceWithMetricReal,
    gt_iff_lt]

@[ext]
class SpaceWithMetricRight (X : Type) extends TopologicalSpace X where
  d : X â†’ X â†’ â„
  dist_eq_zero (x : X) : d x x = 0
  dist_pos (x y : X) : x â‰  y â†’ 0 < d x y
  symm (x y : X) : d x y = d y x
  triangle (x y z : X) : d x z â‰¤ d x y + d y z
  top_eq : âˆ€ S : Set X, _root_.IsOpen S â†” âˆ€ x âˆˆ S, âˆƒ Ï : â„, 0 < Ï âˆ§ {y | d x y < Ï} âŠ† S

instance (X : Type) [SpaceWithMetricRight X] : TopologicalSpace X := inferInstance

attribute [-instance] instTopologicalSpaceOfSpaceWithMetric

instance : SpaceWithMetricRight â„ where
  d := fun x y â†¦ dist x y
  dist_eq_zero := by simp
  dist_pos := fun x y â†¦ (dist_pos).mpr
  symm := dist_comm
  triangle := by exact fun x y z â†¦ dist_triangle x y z
  top_eq := by
    intro S
    rw [Metric.isOpen_iff]
    simp only [instTopologicalSpaceOfSpaceWithMetric, dist_comm, Metric.ball, instSpaceWithMetricReal,
      gt_iff_lt]

#synth TopologicalSpace â„

example : Continuous (fun x : â„ â†¦ x + 1 ) := by
  exact continuous_add_right 1

example : instTopologicalSpaceOfSpaceWithMetricRight â„ = UniformSpace.toTopologicalSpace := rfl


end Exercises
