import Mathlib.CategoryTheory.Equivalence
import Mathlib.CategoryTheory.Limits.IsLimit
import Mathlib.CategoryTheory.NatIso
import Mathlib.CategoryTheory.DiscreteCategory
import Mathlib.CategoryTheory.Category.Preorder

universe u v

section Generalities

/-
How are categories formalized in Lean?

We want the type of morphisms between objects `X` and `Y` to be a type depending on
`X`, `Y`. (I.e., we don't want to have a type of "all morphisms of the category" with
domain and codomain maps to the type of objects.)
-/

structure NonFlatCat where
    Obj : Type u
    Mor : Obj ‚Üí Obj ‚Üí Type v
    id : (X : Obj) ‚Üí Mor X X
    comp : (X : Obj) ‚Üí (Y : Obj) ‚Üí (Z : Obj) ‚Üí (f : Mor X Y) ‚Üí (g : Mor Y Z) ‚Üí Mor X Z
    id_comp : sorry
    comp_id : sorry
    comp_assoc : sorry
-- This is not the actual definition. In fact, as you can see formulating the associativity
-- of composition will get painful if we do it this way.

/-
A few remarks before we see the actual definition:

## Universes:
As we mentioned before, every term has a type. For example:
-/
#check ‚Ñï
#check Bool
#check Prop
#check Type
#check Type 1
#check Type 2
/-
You see in particular that `Type` itself (which is short for `Type 0`) has a type, called
`Type 1`, that `Type 1` has a type called `Type 2`, etc. The natural number that appears
after `Type` is called a *universe*; we can declare a universe with the following command:
`universe u`. We can add take the sum or max of two universes.

There are rules for assigning a universe to the various type constructions. For example,
if `Œ± : Type u` and `Œ≤ : Type v`, then `Œ± ‚Üí Œ≤ : Type (max u v)`. The general idea is that
the usual "set-theoretic" operations will not make you leave a universe.

Universes are introduced to avoid set-theoretic paradoxes like "the set of all sets which
do not have themselves as an element".

There are special rules for terms of type `Prop`:
-/
variable (Œ± : Type u) (R : Œ± ‚Üí Prop)
#check Œ± ‚Üí Prop -- this is the type of sets of `Œ±`, it should be in the same universe as `Œ±`
#check (x : Œ±) ‚Üí R x  -- this is the statement `‚àÄ (x : Œ±), R x` and so it should be of type `Prop`
/-
Internally, there is another hierarchy called `Sort`, for which `Prop` is `Sort 0` and
`Type u` is `Sort (u + 1)`. Let's not worry too much about that, as the rules are
pretty intuitive.

A couple more points about universes:
* Unlike set theory, we do NOT have an inclusion from an universe to the next one.
Instead, we have "universe lifting" operations `ULift`.
-/
#print ULift
/-
This is a bit painful but you get used to it.
* Categories in Lean are "doubly universe polymorphic", which means that we are allowing
objects and morphisms to be in different universes, because why not? The two most common
cases are when objects and morphisms are in the same universe (called `SmallCategory` in
mathlib) and when objects are one universe level higher than morphisms (called `LargeCategory`
in mathlib).


## Equalities

Because morphisms between objects `X` and `Y` are in a type depending on `X` and `Y`,
it is a *very very good idea* to avoid writing equalities between objects at all costs.

It is also good practice in category theory, because it is almost always irrelevant whether
two objects in a category are actually equal, we care whether they are isomorphic. (The
property of being equal is not preserved if you pass to an equivalent category, the property
of being isomorphic is preserved.)

## Canonical stuff

In informal mathematics, we use the word "canonical" a lot without specifying what it means.
We also allow ourselves to talk about "the" product of two objects, "the" limit of a
diagram, "the" left adjoint of a functor etc, because those are "given up to unique
isomorphism".

We cannot do this in Lean. We will have to talk about "a" product, "a" limit etc, and
then we will have uniqueness lemmas about them. Also, a product of `X` and `Y` is not
just an object of the category, it's the data of an object `P`, of morphism
`P ‚ü∂ X` and `P ‚ü∂ Y`, and of a proof of the universal property of these morphisms. Same
for limits, adjoints...

Relatedly, a lot of things that we think of as propositions are actually definitions
in category theory. For example, saying that an object is the limit of a diagram:
-/
#check CategoryTheory.Limits.IsLimit
#print CategoryTheory.Limits.IsLimit

end Generalities

section Definitions

open CategoryTheory

/- So how do we define a category? The way it is defined in mathlib, a
category is a type with a `CategoryTheory.Category` instance on it.
-/
variable (C : Type u) [Category.{v} C]

/- What is the `Category` class?
It's actually built up from two other classes, `Quiver` which contains the
information of the `Hom` types (a quiver is just an oriented graph):
-/
#print Quiver

/- then `CategoryStruct` which extends `Quiver` abd contains the information of the
identities and of the composition law:
@[pp_with_univ]
class CategoryStruct (obj : Type u) extends Quiver.{v + 1} obj : Type max u (v + 1) where
  /-- The identity morphism on an object. -/
  id : ‚àÄ X : obj, Hom X X
  /-- Composition of morphisms in a category, written `f ‚â´ g`. -/
  comp : ‚àÄ {X Y Z : obj}, (X ‚ü∂ Y) ‚Üí (Y ‚ü∂ Z) ‚Üí (X ‚ü∂ Z)
-/
#print CategoryStruct

/-
and finally `Category`, which extends `CategoryStruct` and contains the properties
of the identities and the associativity of composition:

@[pp_with_univ]
class Category (obj : Type u) extends CategoryStruct.{v} obj : Type max u (v + 1) where
  /-- Identity morphisms are left identities for composition. -/
  id_comp : ‚àÄ {X Y : obj} (f : X ‚ü∂ Y), ùüô X ‚â´ f = f := by aesop_cat
  /-- Identity morphisms are right identities for composition. -/
  comp_id : ‚àÄ {X Y : obj} (f : X ‚ü∂ Y), f ‚â´ ùüô Y = f := by aesop_cat
  /-- Composition in a category is associative. -/
  assoc : ‚àÄ {W X Y Z : obj} (f : W ‚ü∂ X) (g : X ‚ü∂ Y) (h : Y ‚ü∂ Z), (f ‚â´ g) ‚â´ h = f ‚â´ g ‚â´ h := by
    aesop_cat
-/
#print Category

/-
A few notational points.
-/
variable (X Y Z T : C) (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ T)

#check ùüô X  -- the identity morphism of `X`: \ + b + 1

#check X ‚ü∂ Y -- the type of morphisms from `X` to `Y`
-- Warning: the arrow is typed using "\ + hom" and is different from the "function" arrow ‚Üí !

#check f ‚â´ g  -- the composition of `f` and `g` (‚â´ = \ + gg)
-- Note that composition is written in diagrammatic order! You get used to it.

example : ùüô X ‚â´ f ‚â´ g = f ‚â´ ùüô Y ‚â´ g := by simp

/- Composition associates on the right.-/
#check f ‚â´ g ‚â´ h

example : f ‚â´ g ‚â´ h = f ‚â´ (g ‚â´ h) := by rfl

example : f ‚â´ g ‚â´ h = (f ‚â´ g) ‚â´ h := by rw [Category.assoc] --`rfl` does not work

/-
We have a special tactic for proving "obvious" categorical stuff. It's called `aesop_cat`.
Among other things, it will apply all `simp` lemmas and also try to use `ext` tactics.

You can see it in the fields of `Category`:
@[pp_with_univ]
class Category (obj : Type u) extends CategoryStruct.{v} obj : Type max u (v + 1) where
  /-- Identity morphisms are left identities for composition. -/
  id_comp : ‚àÄ {X Y : obj} (f : X ‚ü∂ Y), ùüô X ‚â´ f = f := by aesop_cat
  /-- Identity morphisms are right identities for composition. -/
  comp_id : ‚àÄ {X Y : obj} (f : X ‚ü∂ Y), f ‚â´ ùüô Y = f := by aesop_cat
  /-- Composition in a category is associative. -/
  assoc : ‚àÄ {W X Y Z : obj} (f : W ‚ü∂ X) (g : X ‚ü∂ Y) (h : Y ‚ü∂ Z), (f ‚â´ g) ‚â´ h = f ‚â´ g ‚â´ h := by
    aesop_cat

This means that, when you define a category, if `aesop_cat` can prove the fields
`id_comp`, `comp_id` or `assoc`, then you don't need to include them.

For example:
-/

def CatType := Type u

instance : Category CatType where
  Hom X Y := X ‚Üí Y
  id X := fun a ‚Ü¶ a
  comp f g := g ‚àò f
-- Success!
-- Of course this instance already exists:
#synth Category (Type u)  -- This is a `LargeCategory` instance.

/-
After categories, the next step is functors.
-/
#print Functor -- careful, there is also a `_root_.Functor`!


/-
They are of course defined in two steps:
A `Prefunctor` is a morphism of quivers, i.e.:

structure Prefunctor (V : Type u‚ÇÅ) [Quiver.{v‚ÇÅ} V] (W : Type u‚ÇÇ) [Quiver.{v‚ÇÇ} W] where
  /-- The action of a (pre)functor on vertices/objects. -/
  obj : V ‚Üí W
  /-- The action of a (pre)functor on edges/arrows/morphisms. -/
  map : ‚àÄ {X Y : V}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y)

It is written `C ‚•§q D`. (\ + func + q)

A functor is a morphism of quivers that preserves identities and compositions:

structure Functor (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D]
    extends Prefunctor C D : Type max v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ where
  /-- A functor preserves identity morphisms. -/
  map_id : ‚àÄ X : C, map (ùüô X) = ùüô (obj X) := by aesop_cat
  /-- A functor preserves composition. -/
  map_comp : ‚àÄ {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z), map (f ‚â´ g) = map f ‚â´ map g := by aesop_cat

It is written `C ‚•§ D` (\ + func).
-/

variable {C : Type u} [Category.{v} C] (X : C)

-- The Yoneda functors:

example : C ‚•§ Type v where
  obj Y := X ‚ü∂ Y
  map f g := g ‚â´ f

example : C·µí·µñ ‚•§ Type v where
  obj Y := Opposite.unop Y ‚ü∂ X
  map f g := sorry
  map_id := sorry
  map_comp := sorry

#check Opposite.op
#check Opposite.unop
#check Quiver.Hom.op
#check Quiver.Hom.unop

variable {D : Type*} [Category D]

example (F : C ‚•§ D) : C·µí·µñ ‚•§ D·µí·µñ where
  obj := sorry
  map := sorry
  map_id := sorry
  map_comp := sorry

/-
We can compose functors, and this is written `F ‚ãô G` (\ + ggg), if `F : C ‚•§ D`
and `G : D ‚•§ E` (so diagrammatic order again).
The identity functor is `ùü≠ C` (\ + sb1).
-/

/-
It is not a good idea in general to write equality between functors, because that
means writing equalities between objects.

For example, while it is technically true that composition of functors is associative:
-/
example {E E' : Type*} [Category E] [Category E'] (F : C ‚•§ D) (G : D ‚•§ E) (H : E ‚•§ E') :
    (F ‚ãô G) ‚ãô H = F ‚ãô G ‚ãô H := rfl
/-
it is still much better to use `Functor.associator`:
-/
#check Functor.associator

example {E E' : Type*} [Category E] [Category E'] (F : C ‚•§ D) (G : D ‚•§ E) (H : E ‚•§ E') :
    (F ‚ãô G) ‚ãô H ‚âÖ F ‚ãô G ‚ãô H := Functor.associator F G H
-- `‚âÖ` (\ + iso) is the symbol for an isomorphism; here it is a natural isomorphism.

-- Similary for composition with identity functors:
#check Functor.leftUnitor
#check Functor.rightUnitor

/-
And finally, we have natural transformations (and natural isomorphisms):
-/
#print NatTrans
/-
@[ext]
structure NatTrans {C : Type u‚ÇÅ} [Category.{v‚ÇÅ, u‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ, u‚ÇÇ} D]
    (F G : C ‚•§ D) : Type max u‚ÇÅ v‚ÇÇ where
  /-- The component of a natural transformation. -/
  app : ‚àÄ X : C, F.obj X ‚ü∂ G.obj X
  /-- The naturality square for a given morphism. -/
  naturality : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), F.map f ‚â´ app Y = app X ‚â´ G.map f := by aesop_cat
-/

/-
The type of natural transformations from `F` to `G` is writte `F ‚ü∂ G` (with the `hom` arrow).
-/

#print NatIso
-- oops... A `NatIso` is actually just an isomorphism in the category of functors.
#print Iso
#synth Category (C ‚•§ D)
-- The category of functors from `C` to `D`, whose objects are functors `C ‚•§ D` and
-- whose morphisms are natural transformations, with vertical composition (denoted by
-- `‚â´`, just like ordinary composition).

/-
We also have horizontal composition of natural transformations:

def CategoryTheory.NatTrans.hcomp {C : Type u‚ÇÅ} [Category.{v‚ÇÅ, u‚ÇÅ} C] {D : Type u‚ÇÇ}
    [Category.{v‚ÇÇ, u‚ÇÇ} D] {E : Type u‚ÇÉ} [Category.{v‚ÇÉ, u‚ÇÉ} E]
    {F G : Functor C D} {H I : Functor D E} (Œ± : F ‚ü∂ G) (Œ≤ : H ‚ü∂ I) :
  F.comp H ‚ü∂ G.comp I
-/

end Definitions

section Examples

open CategoryTheory

/- There are some categories already in mathlib.-/

-- Discrete categories
variable (J : Type*)

#check Discrete J

#print Discrete
/-
@[ext, aesop safe cases (rule_sets := [CategoryTheory])]
structure Discrete (J : Type u‚ÇÅ) where
  /-- A wrapper for promoting any type to a category,
  with the only morphisms being equalities. -/
  as : J
-/
-- `Discrete J` is basically `J`. More precisely, it is a structure with a unique field
-- whose value is a term of type `J`. (Note that `Opposite` was constructed similarly.)
example (x : J) : Discrete J := {as := x}

example (x : J) : Discrete J := Discrete.mk x

example {x : J} : Discrete.mk x = {as := x} := rfl

#synth Category (Discrete J)

example (C : Type*) [Category C] (f : J ‚Üí C) : Discrete J ‚•§ C where
  obj := sorry
  map := sorry
  map_id := sorry
  map_comp := sorry

-- Preorders as categories

variable [Preorder J]

#synth Category J
-- The category whose objects are terms of type `J`, such that there is a morphism
-- `i ‚ü∂ j` if and only if `i ‚â§ j`.

#print Preorder.smallCategory
/-
instance (priority := 100) smallCategory (Œ± : Type u) [Preorder Œ±] : SmallCategory Œ± where
  Hom U V := ULift (PLift (U ‚â§ V))
  id X := ‚ü®‚ü®le_refl X‚ü©‚ü©
  comp f g := ‚ü®‚ü®le_trans _ _ _ f.down.down g.down.down‚ü©‚ü©

Yikes! The horrible definition of `Hom U V` is because we don't allow `Hom` types to be
in `Prop`, and `U ‚â§ V` is.
-/
#check PLift

example (K : Type*) [Preorder K] (f : J ‚Üí K) (h : Monotone f) : J ‚•§ K where
  obj := sorry
  map := sorry
  map_id := sorry
  map_comp := sorry

-- The category of types (in a fixed universe), which we already met:

#synth Category (Type u)

-- Morphisms are defined as functions:
example {X Y : Type u} (f : X ‚Üí Y) : X ‚ü∂ Y := f

-- If Lean doesn't understand, you can use `asHom`:
example {X Y : Type u} (f : X ‚Üí Y) : X ‚ü∂ Y := asHom f


/- Concrete categories: These are subcategories of the category of types,
i.e. categories `C` that come with a faithful functor `forget C : C ‚•§ Type u`.

We can also have two concrete categories `C` and `D` with a forgetful functor
from `C` to `D`, i.e. a functor `forget‚ÇÇ C D : C ‚•§ D` such that
`forget‚ÇÇ C D ‚ãô forget D = forget C` (this is an equality of functors, but is
is written like this in mathlib.)

This includes all algebraic categories, as well as `TopCat`.

-/




end Examples
