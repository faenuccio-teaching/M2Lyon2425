import Mathlib.Algebra.Category.Grp.Basic
import Mathlib.CategoryTheory.Limits.EssentiallySmall
import Mathlib.CategoryTheory.Limits.Shapes.Types
import Mathlib.CategoryTheory.Limits.Shapes.WideEqualizers
import Mathlib.CategoryTheory.Subobject.Comma

open CategoryTheory Category

universe u u' v v' w w'

variable {C : Type u} [Category.{v} C]

variable {J : Type w} [Category.{w} J]

section Generalities
/-
Limits and colimits.

Let `K : J ‚•§ C` be a functor. We say that an object `X` of `C` is a limit of `F`
if:
(1) We have morphisms `f j : X ‚ü∂ F.obj j` for every `j : J`.
(2) For every morphism `u : j ‚ü∂ k` in `J`, we have `f j ‚â´ F.map u = f k`.
(3) Every time we have the data of an object `Y` of `C` and of morphisms
`g j : Y ‚ü∂ F.obj j` such that `g j ‚â´ F.map u = g k` for every morphism
`u : j ‚ü∂ k` in `J`, there exists a unique `Œ± : Y ‚ü∂ X` such that
`g j = Œ± ‚â´ f j` for every `j : J`

It will be convenient to package all the information given in (1) and (2)
into something that we will call a `Cone` of `F`. We can make cones into
a category in the "obvious" way, and a limit will be a terminal cone.

Before we do that, let's look at a few examples:

* Terminal objects:
A terminal object of `C` is a limit of the unique functor from the
empty category to `C`. The data of (1) and (2) then collapses to
just "an object `X` of `C`", and condition (3) says: for every
`Y : C`, there exists a unique morphism `Y ‚ü∂ X`.

For example, in `Type u` every one-point type is a terminal object.
-/

#check Limits.isTerminalEquivUnique
#check Limits.IsTerminal.ofUnique

/-
* Products (cf. Mathlib.CategoryTheory.Limits.Shapes.Products):
This is the case where `J` is a discrete category. Giving `F` is
equivalent to giving the function `F.obj : J ‚Üí C`. The data of (1)
and (2) becomes "an object `X` of `C` and a morphism `p j : X ‚ü∂ F.obj j`
for every `j : J`".

Property (3) says: every time we have an object `Y` and morphisms
`q j : Y ‚ü∂ F.obj j`, there exists a unique `Œ± : X ‚ü∂ Y` such that
`q j = Œ± ‚â´ p j`.

In the category `Type u` (and algebraic categories like
`Grp`, `AddCommGrp`, `ModuleCat` etc), the usual products are
categorical products.

* A special case of products is that of binary products. This is
when `J` has exactly two objects an dis discrete.
The data of `F` is equivalent
to that of two objects `A, B : C`. The data of (1) and (2)
is equivalent to that of `X : C` and of two maps
`p : X ‚ü∂ A` and `q : X ‚ü∂ B`. Condition (3) says that, if
we have `Y : C` and two maps `p' : Y ‚ü∂ A` and `q' : Y ‚ü∂ B`,
then there exists a unique `Œ± : Y ‚ü∂ X` such that `p' = Œ± ‚â´ p`
and `q' = Œ± ‚â´ q`.

In `Type u` (and algebraic categories like
`Grp`, `AddCommGrp`, `ModuleCat` etc),
Cartesian products are binary products.

In mathlib, the type with two elements used for binary products
(and binary coproducts) is called `WalkingPair`, its two
elements are called `left` and `right`.
-/
#print Limits.WalkingPair

/-
So the category `J` will be `Discrete (WalkingPair)` (if we opened
the `Limits` namespace).
-/

/-
* Fiber products:
This is the case where `J` is a preordered type with three elements
`left, right, none` such that `left < none`, `right < none` and
`left, right` are not comparable. This category is called
`WalkingCospan` in mathlib.
-/
#print Limits.WalkingCospan

/-
Giving a functor `K : J ‚•§ C` is equivalent to giving two morphisms
`f : A ‚ü∂ C`, `g : B ‚ü∂ C` with the same target. The data of
(1) and (2) is equivalent to that of `X : C` and two morphisms
`p : X ‚ü∂ A`, `q : X ‚ü∂ B` such that `p >> f = q >> g`.

Condition (3) says that, if
we have `Y : C` and two maps `p' : Y ‚ü∂ A` and `q' : Y ‚ü∂ B`
such that `p' ‚â´ f = q' ‚â´ g`,
then there exists a unique `Œ± : Y ‚ü∂ X` such that `p' = Œ± ‚â´ p`
and `q' = Œ± ‚â´ q`.

In `Type u` (and algebraic categories like
`Grp`, `AddCommGrp`, `ModuleCat` etc), the usual fiber
products are categorical fiber
products: if `f : A ‚ü∂ C` and `g : B ‚ü∂ C`, their usual fiber
product if `{(x, y) : A √ó B | f x = g y}`.


* Equalizers:
This is the special case of fiber products where the
morphisms `f, g` have the same source. So the category
`J` has two distinct objects `zero` and `one`, and two
distinct morphisms from `zero` to `one`.

This category is called `WalkingParallelPair` in mathlib,
and we can construct functors from it using
`parallelPair`. (Note that this the first example
where `J` is not a preorder.)
-/
#print Limits.WalkingParallelPair

#synth Category Limits.WalkingParallelPair

example {A B : C} (f g : A ‚ü∂ B) : Limits.WalkingParallelPair ‚•§ C :=
  Limits.parallelPair f g

/-
In `Type u` (and algebraic categories like
`Grp`, `AddCommGrp`, `ModuleCat` etc), the equalizer of
`f, g : A ‚ü∂ B` is given by `{x : A | f x = g x}`.

* Kernels

If `C` has a notion of zero morphisms, then the kernel of
`f : A ‚ü∂ B` is the equalizer of `f` and of the zero morphism
`0 : A ‚ü∂ B`.

For example, in `AddCommGrp`, the kernel of `f : A ‚ü∂ B` is
`{x : A | f x = 0}`.
-/

/-
There are other special cases, you can find them in
`Mathlib.CategoryTheory.Limits.Shapes`.
-/

/-
Coming back to the general definition of limits, we first
introduce cones over `F`: a term of type `Cone F` is
equivalent to the data of points (1) and (2) above.
The object `X` of (1) is called the *point* of the cone.
-/

#print Limits.Cone

/-
A `c : Cone F` is:
* an object `c.pt` and
* a natural transformation `c.œÄ : c.pt ‚ü∂ F` from the constant `c.pt` functor to `F`.

So the data required to make a term of type `Cone F` is morphisms
`œÄ‚±º : c.pt ‚ü∂ K j` for all `j : J` such that, for every morphism
`u : j ‚ü∂ k` in `J`, we have`œÄ·µ¢ ‚â´ F.map u = œÄ‚±º`.

structure Cone (K : J ‚•§ C) where
  /-- An object of `C` -/
  pt : C
  /-- A natural transformation from the constant functor at `X` to `F` -/
  œÄ : (const J).obj pt ‚ü∂ F
-/

example (K : J ‚•§ C) (c: Limits.Cone K) {j k : J} (u : j ‚ü∂ k) : 0 = 0 := by
  have := c.pt
  have := c.œÄ
  have := c.œÄ.app j
  have := c.w u
  rfl

/-
A morphism of cones `c ‚ü∂ c'` is a morphism between their points that makes
all the obvious diagrams commute, i.e. a `f : c.pt ‚ü∂ c'.pt` such that,
for every `j : J`, we have `f ‚â´ c'.œÄ.app j = c.œÄ.app j`.
-/

example (K : J ‚•§ C) (c c' : Limits.Cone K) (f : c ‚ü∂ c') : 0 = 0 := by
  have := f.hom
  have := f.w
  rfl

/-
We cannot talk about *the* limit of a functor, because the limits is
only unique up to unique isomorphism (it satisfies a universal property);
also, being a limit is a property of the cone, not just its point.
So we define a structure `IsLimit` that says that a cone is a limit cone.
-/
#check Limits.IsLimit

/-
/-- A cone `t` on `F` is a limit cone if each cone on `F` admits a unique
cone morphism to `t`. -/

structure IsLimit (t : Cone F) where
  /-- There is a morphism from any cone point to `t.pt` -/
  lift : ‚àÄ s : Cone F, s.pt ‚ü∂ t.pt
  /-- The map makes the triangle with the two natural transformations commute -/
  fac : ‚àÄ (s : Cone F) (j : J), lift s ‚â´ t.œÄ.app j = s.œÄ.app j := by aesop_cat
  /-- It is the unique such map to do this -/
  uniq : ‚àÄ (s : Cone F) (m : s.pt ‚ü∂ t.pt) (_ : ‚àÄ j : J, m ‚â´ t.œÄ.app j = s.œÄ.app j), m = lift s := by
    aesop_cat
-/

/-
Then we have theorems saying that a limit cone is unique up to
(unique) isomorphism.
-/

#check Limits.IsLimit.uniqueUpToIso

/-
Colimits are "the same" except that you revert the direction of
every morphism. So we get a categorical definition of initial objects
(the empty type for types, the trivial group/abelian group/module, etc),
coproducts (also called "disjoint union" for types, "free products" for groups,
and "direct sums" for modules), binary coproducts, pushouts,
coequalizers, cokernels etc.

Unlike limits, colimits generally have different definitions in
the various algebraic categories.
-/

end Generalities

/- Let's do some exercises to learn to manipulate (co)limits, and
let's open the namespace `Limits` to shorten the declarations.
-/

open Limits

variable {D : Type u'} [Category.{v'} D] (G : D ‚•§ C)

section Preserves
/- Here we prove that a right adjoint functor preserves all limits.
We also want to show this for big limits, so we introduce more universes
for the indexing category of the limits.
-/

#check Functor.leftAdjoint
#check Adjunction.ofIsRightAdjoint

noncomputable def RightAdjointPreservesLimit [G.IsRightAdjoint]
    {J : Type w} [Category.{w'} J] (K : J ‚•§ D) : PreservesLimit K G where
  preserves {c} (hc) := by
    set adj := Adjunction.ofIsRightAdjoint G
    refine IsLimit.mk (fun s ‚Ü¶ ?_) (fun s j ‚Ü¶ ?_) (fun s m eq ‚Ü¶ ?_)
    ¬∑ dsimp
      refine (adj.homEquiv _ _).toFun ?_
      set c':= (Cones.postcompose (whiskerLeft K adj.counit)).obj (G.leftAdjoint.mapCone s)
      exact hc.lift c'
    ¬∑ dsimp
      rw [‚Üê Adjunction.homEquiv_naturality_right]
      simp
    ¬∑ dsimp
      refine (adj.homEquiv _ _).symm.injective ?_
      simp
      refine hc.hom_ext (fun j ‚Ü¶ ?_)
      refine (adj.homEquiv _ _).injective ?_
      simp
      exact eq j

end Preserves

-- This is a lemma from `Categories1`.
lemma IsRightAdjointIffInitial :
    G.IsRightAdjoint ‚Üî ‚àÄ (X : C),
    HasInitial (StructuredArrow X G) := sorry

section GeneralAdjointFunctorTheorem

#check hasInitial_of_unique

lemma InitialOfFamily [HasLimits C] {I : Type*} (x : I ‚Üí C)
    (wi : ‚àÄ (X : C), ‚àÉ (i : I), Nonempty (x i ‚ü∂ X)) [Small.{v} I] :
    HasInitial C := by
  have := Discrete.essentiallySmallOfSmall (Œ± := I)
  have := hasLimitsOfShape_of_essentiallySmall (Discrete I) C
  set X := ‚àè·∂ú x
  set Y := wideEqualizer (fun (u : X ‚ü∂ X) ‚Ü¶ u)
  have IX : ‚àÄ (A : C), Inhabited (X ‚ü∂ A) := by
    intro A
    refine Inhabited.mk ?_
    set i := Classical.choose (wi A)
    set u := Classical.choice (Classical.choose_spec (wi A))
    set g : X ‚ü∂ x i := Pi.œÄ x i
    exact g ‚â´ u
  have : ‚àÄ (A : C), Unique (Y ‚ü∂ A) := by
    intro A
    refine @Unique.mk' _ ?_ ?_
    ¬∑ exact Inhabited.mk (limit.œÄ (parallelFamily
      (fun (u : X ‚ü∂ X) ‚Ü¶ u)) WalkingParallelFamily.zero ‚â´ (IX A).default)
    ¬∑ refine Subsingleton.intro (fun f g ‚Ü¶ ?_)
      set Z := equalizer f g
      set j : Z ‚ü∂ Y := limit.œÄ (parallelPair f g) WalkingParallelPair.zero
      set k := (IX Z).default
      set i : Y ‚ü∂ X := wideEqualizer.Œπ (fun (u : X ‚ü∂ X) ‚Ü¶ u)
      have eq : i ‚â´ k ‚â´ j ‚â´ i = i := by
        conv_rhs => rw [‚Üê Category.comp_id i]
        exact wideEqualizer.condition (f := fun (u : X ‚ü∂ X) ‚Ü¶ u) _ _
      have : Mono i := inferInstance
      have eq' : (i ‚â´ k) ‚â´ j = ùüô _ := by
        rw [‚Üê cancel_mono i, Category.assoc, Category.assoc, Category.id_comp]
        exact eq
      have : Epi j := epi_of_epi_fac eq'
      exact eq_of_epi_equalizer
  exact hasInitial_of_unique Y

/-- The solution set condition : for every `X : C`, there exists a
small familly `y : I ‚Üí D` and morphisms `f i : X ‚ü∂ G.obj (y i)` such that
each morphism `X ‚ü∂ G.obj Y` factors as `f i ‚â´ G.map g` for some `i` and
some `g : y i ‚ü∂ Y`.
-/
def SolutionSetCondition := ‚àÄ (X : C), ‚àÉ (I : Type u) (_ : Small.{v'} I)
  (y : I ‚Üí D) (f : (i : I) ‚Üí (X ‚ü∂ G.obj (y i))), ‚àÄ (Y : D) (h : X ‚ü∂ G.obj Y),
  ‚àÉ (i : I) (g : y i ‚ü∂ Y), h = f i ‚â´ G.map g

variable (X : C)
#synth Category (StructuredArrow X G)

/--
The general adjoint functor theorem: if `G` satisfies the solution set
condition and preserves small limits, and if `C` and `D` have all small limits,
then `G` has a left adjoint.
-/
theorem GeneralAdjointFunctor [HasLimits D]
    [PreservesLimitsOfSize.{v', v'} G]
    (h : SolutionSetCondition G) : G.IsRightAdjoint := by
  rw [IsRightAdjointIffInitial]
  intro X
  obtain ‚ü®I, _, y, f, h‚ü© := h X
  set a : I ‚Üí StructuredArrow X G := fun i ‚Ü¶ StructuredArrow.mk (f i)
  refine InitialOfFamily a ?_
  intro b
  obtain ‚ü®i, g, comm‚ü© := h b.right b.hom
  use i
  exact Nonempty.intro (StructuredArrow.homMk g comm.symm)


end GeneralAdjointFunctorTheorem


section SpecialAdjointFunctorTheorem

/-
Let `X : C`. We have the category `MonoOver X` of monomorphisms `Y ‚ü∂ X`.
Note that `i : Y ‚ü∂ X` and `i' : Y' ‚ü∂ X` are isomorphic in `MonoOver X`
if and only if there exists an isomorphism `u : Y ‚ü∂ Y'`
such that `i = u ‚â´ i'`.

The category of subobjects of `X` is a skeleton of `MonoOver X`, i.e.
it is a subcategory that contains exactly one object in each isomorphism
class.
-/

#print Subobject

/- A category `C` is well-powered relative to a
universe `w` if it is locally small and `Subobject X` is `w`-small for every `X`.
Here "locally small" means that the hom type `X ‚ü∂ Y` is `w`-small for all
`X, Y : C`.
-/

/- If `C` has small limits and `X : C` is such that `Subobject X` is `v`-small,
then we can form a "minimal subobject" of `X` by taking the limit of
all subobjects of `X`. We will actually take the limit over `MonOver X`
(which is equivalent to `Subobject X`), as this is easier.
-/

local instance [HasLimits C] (X : C) [Small.{v} (Subobject X)] :
    EssentiallySmall.{v} (MonoOver X) :=
  (essentiallySmall_monoOver_iff_small_subobject X).mpr inferInstance

local instance [HasLimits C] (X : C) [Small.{v} (Subobject X)] :
    HasLimitsOfShape (MonoOver X) C :=
  hasLimitsOfShape_of_essentiallySmall (MonoOver X) C

noncomputable def MinimalSubobject [HasLimits C] (X : C)
    [Small.{v} (Subobject X)] : C := limit (MonoOver.forget X ‚ãô Over.forget X)

/--
The canonical monomorphism `MinimalSubobject X ‚ü∂ X`.
-/
noncomputable def MinimalSubobjectTo [HasLimits C] (X : C)
    [Small.{v} (Subobject X)] : MinimalSubobject X ‚ü∂ X :=
  limit.œÄ (MonoOver.forget X ‚ãô Over.forget X) (MonoOver.mk' (ùüô X))

local instance [HasLimits C] (X : C)
    [Small.{v} (Subobject X)] : Mono (MinimalSubobjectTo X) where
  right_cancellation {Y} f g eq := by
    dsimp [MinimalSubobject]
    ext j
    have : Mono (j.1.hom) := j.2
    rw [‚Üê cancel_mono j.1.hom]
    simp only [Functor.id_obj, Functor.const_obj_obj, Functor.comp_obj, Over.forget_obj,
      MonoOver.forget_obj_left, Category.assoc]
    have := limit.w (MonoOver.forget X ‚ãô Over.forget X) (j := j)
      (j' := MonoOver.mk' (ùüô X)) (MonoOver.homMk j.1.hom)
    change _ ‚â´ j.1.hom = MinimalSubobjectTo X at this
    rw [this]
    exact eq

/-- The minimality property: every monomorphism into `MinimalSubobject X`
is an isomorphism. Indeed, if `Y ‚ü∂ MinimalSubobject X` is a monormorphism,
then we get a subobject of `X` by taking the composition
`Y ‚ü∂ MinimalSubobject X ‚ü∂ X1.
-/
lemma MinimalSubobjectIsMinimal [HasLimits C] (X : C)
    [Small.{v} (Subobject X)] {Y : C} (u : Y ‚ü∂ MinimalSubobject X)
    [Mono u] : IsIso u := by
  set j := MonoOver.mk' (u ‚â´ MinimalSubobjectTo X)
  set v : MinimalSubobject X ‚ü∂ Y := limit.œÄ (MonoOver.forget X ‚ãô Over.forget X) j
  have eq : u ‚â´ v = ùüô Y := by
    have : Mono (j.1.hom) := j.2
    rw [‚Üê cancel_mono j.1.hom]
    dsimp [v]
    have := limit.w (MonoOver.forget X ‚ãô Over.forget X) (j' := MonoOver.mk' (ùüô X)) (MonoOver.homMk j.1.hom)
    change _ ‚â´ j.1.hom = MinimalSubobjectTo X at this
    rw [Category.assoc, this, Category.id_comp]
    rfl
  have eq' : v ‚â´ u = ùüô (MinimalSubobject X) := by
    rw [‚Üê cancel_mono (MinimalSubobjectTo X)]
    dsimp [v]
    rw [Category.assoc, Category.id_comp]
    have := limit.w (MonoOver.forget X ‚ãô Over.forget X) (j' := MonoOver.mk' (ùüô X)) (MonoOver.homMk j.1.hom)
    change _ ‚â´ u ‚â´ MinimalSubobjectTo X = _ at this
    rw [this]
    rfl
  exact {out := ‚ü®v, eq, eq'‚ü©}

/--
A cogenerating family is a family `x : I ‚Üí C` of objects of `C` such that,
for all `X,Y : C` and `f,g : X ‚ü∂ Y`, if `f ‚â´ h = g ‚â´ h` for every `i : I`
and every `h : Y ‚ü∂ x i`, then `f = g`.
-/
def IsCogenerating {I : Type*} (x : I ‚Üí C) := ‚àÄ {X Y : C} (f g : X ‚ü∂ Y),
  f = g ‚Üî ‚àÄ (i : I) (h : Y ‚ü∂ x i), f ‚â´ h = g ‚â´ h

variable {I : Type*} (x : I ‚Üí C)

local instance [Small.{v} I] [HasLimits C] :
    HasLimitsOfShape (Discrete I) C :=
  letI := Discrete.essentiallySmallOfSmall (Œ± := I)
  hasLimitsOfShape_of_essentiallySmall (Discrete I) C

noncomputable abbrev CogeneratingMono [Small.{v} I] [HasLimits C] (X : C) :
    X ‚ü∂ ‚àè·∂ú (fun (i : I) ‚Ü¶ (‚àè·∂ú fun (_ : X ‚ü∂ x i) ‚Ü¶ x i)) :=
  Pi.lift (fun _ ‚Ü¶ (Pi.lift (fun u ‚Ü¶ u)))

lemma CogeneratingMonoIsMono [Small.{v} I] (h : IsCogenerating x)
    [HasLimits C] (X : C) : Mono (CogeneratingMono x X) where
  right_cancellation {Y} f g eq := by
    rw [h]
    intro i u
    apply_fun (fun k ‚Ü¶ k ‚â´ Pi.œÄ (fun i ‚Ü¶ ‚àè·∂ú fun _ ‚Ü¶ x i) i) at eq
    rw [Category.assoc, Pi.lift_œÄ (f := fun (i : I) ‚Ü¶ (‚àè·∂ú fun (_ : X ‚ü∂ x i) ‚Ü¶ x i))
      (fun _ ‚Ü¶ (Pi.lift (fun u ‚Ü¶ u))) i, Category.assoc,
      Pi.lift_œÄ (f := fun (i : I) ‚Ü¶ (‚àè·∂ú fun (_ : X ‚ü∂ x i) ‚Ü¶ x i))
      (fun _ ‚Ü¶ (Pi.lift (fun u ‚Ü¶ u))) i] at eq
    apply_fun (fun k ‚Ü¶ k ‚â´ Pi.œÄ (fun _ ‚Ü¶ x i) u) at eq
    rw [Category.assoc,
      Pi.lift_œÄ (f := fun (_ : X ‚ü∂ x i) ‚Ü¶ x i) (fun u ‚Ü¶ u) u,
      Category.assoc,
      Pi.lift_œÄ (f := fun (_ : X ‚ü∂ x i) ‚Ü¶ x i) (fun u ‚Ü¶ u) u] at eq
    exact eq

/--
We prove that if `C` is well-powered relative to `v` (the universe
level of its hom types) and has all `v`-small limits and a small
cogenerating family, then it admits an initial object.
(We will later apply this to comma categories `StructuredArrow X G`).
-/
lemma HasInitialOfWellPowered [WellPowered.{v} C] [HasLimits C] {I : Type*}
    [Small.{v} I] (x : I ‚Üí C) (h : IsCogenerating x) :
    HasInitial C := by
  have := Discrete.essentiallySmallOfSmall (Œ± := I)
  have := hasLimitsOfShape_of_essentiallySmall (Discrete I) C
  set X := ‚àè·∂ú x
  set e := MinimalSubobject X
  have : ‚àÄ (A : C), Subsingleton (e ‚ü∂ A) := by
    intro _
    refine Subsingleton.intro (fun f g ‚Ü¶ ?_)
    have : IsIso (equalizer.Œπ f g) := MinimalSubobjectIsMinimal _ _
    exact eq_of_epi_equalizer
  have : ‚àÄ (A : C), Inhabited (e ‚ü∂ A) := by
    intro A
    set u : ‚àè·∂ú x ‚ü∂ ‚àè·∂ú (fun (i : I) ‚Ü¶ (‚àè·∂ú fun (_ : A ‚ü∂ x i) ‚Ü¶ x i)) :=
      Pi.map (fun _ ‚Ü¶ (Pi.lift (fun _ ‚Ü¶ ùüô _)))
    set B := pullback (CogeneratingMono x A) u
    have := CogeneratingMonoIsMono x h A
    --have : Mono (pullback.snd (CogeneratingMono x A) u) := inferInstance
    set j : MonoOver X := MonoOver.mk' (pullback.snd (CogeneratingMono x A) u)
    set v : e ‚ü∂ B := limit.œÄ (MonoOver.forget X ‚ãô Over.forget X) j
    exact Inhabited.mk (v ‚â´ pullback.fst (CogeneratingMono x A) u)
  have : ‚àÄ (A : C), Unique (e ‚ü∂ A) := fun A ‚Ü¶ Unique.mk' _
  exact hasInitial_of_unique e

lemma IsCogeneratingOfIsCogenerating (y : I ‚Üí D) (h : IsCogenerating y) (X : C) :
    IsCogenerating (fun (a : Œ£ (i : I), X ‚ü∂ G.obj (y i)) ‚Ü¶
    StructuredArrow.mk a.2) := by
  intro a b u v
  rw [StructuredArrow.hom_eq_iff, h]
  refine ‚ü®fun h ‚Ü¶ (fun i f ‚Ü¶ ?_), fun h ‚Ü¶ (fun i f ‚Ü¶ ?_)‚ü©
  ¬∑ rw [StructuredArrow.hom_eq_iff, StructuredArrow.comp_right,
      StructuredArrow.comp_right]
    exact h i.1 f.right
  ¬∑ have := h ‚ü®i, b.hom ‚â´ G.map f‚ü© (StructuredArrow.homMk f (by simp))
    exact (StructuredArrow.hom_eq_iff _ _).mp this

#check StructuredArrow.hasLimitsOfSize

theorem SpecialAdjointFunctor [HasLimits D] [WellPowered.{v'} D]
    [PreservesLimitsOfSize.{v', v'} G] [LocallySmall.{v'} C]
    (cogen : ‚àÉ (I : Type*) (y : I ‚Üí D) (_ : Small.{v'} I), IsCogenerating y) :
    G.IsRightAdjoint := by
  rw [IsRightAdjointIffInitial]
  intro X
  obtain ‚ü®I, y, hs, h‚ü© := cogen
  have : PreservesFiniteLimits G := PreservesLimitsOfSize.preservesFiniteLimits G
--  have : WellPowered.{v'} (StructuredArrow X G) := inferInstance
--  have : HasLimitsOfSize.{v', v'} (StructuredArrow X G) := inferInstance
  have : ‚àÄ (a : I), Small.{v', v} (X ‚ü∂ G.obj (y a)) := by
    intro a
    infer_instance
--  have : Small.{v'} ((i : I) √ó (X ‚ü∂ G.obj (y i))) := inferInstance
  exact HasInitialOfWellPowered _ (IsCogeneratingOfIsCogenerating G y h X)

end SpecialAdjointFunctorTheorem

section GroupObject

/-
To play with limits some more, we will look at the notion of
group objects in a category `C` that admits finite products.
This is a categorical definition of groups.

The idea is that a group object in `C` is the data of:
- An object of `C`, let's call it `X`.
- A "multiplication" map, which should be a morphism `X ‚®Ø X ‚ü∂ X`.
(Here `‚®Ø` is a notation for the categorical product, typed using
"\ + X".)
- An "inversion" map, which should be a morphism `X ‚ü∂ X`.
- A "unit": If `X` were a type, the unit would be an element of
`X`, but this does not make sense here. Instead, we will ask
for a morphism `‚ä§_ C ‚ü∂ X`, where `‚ä§_ C` is a terminal object of `C`.
(If `C` is `Type u`, then `‚ä§_ C` is a one-point type, so giving a
morphism `‚ä§_ C ‚ü∂ X` is the same as giving an element of `X`.)
(Type `‚ä§` using "\ + top".)

Of course, these data should satisfy some axioms, such as associativity
of multiplication, the fact that the unit is a unit, etc. The insight
is that we can express all of these axioms categorically, as
equalities betweem some morphisms.
-/

variable (C : Type u) [Category.{v} C]

variable [HasFiniteProducts C]

structure GroupObject where
  X : C
  mul : X ‚®Ø X ‚ü∂ X    -- type ‚®Ø using \ + X
  mul_assoc : prod.map mul (ùüô X) ‚â´ mul =
      (prod.associator X X X).hom ‚â´ prod.map (ùüô X) mul ‚â´ mul
  one : ‚ä§_ C ‚ü∂ X -- type `‚ä§` using \ + top
  one_mul : (prod.leftUnitor X).inv ‚â´ prod.map one (ùüô X) ‚â´ mul = ùüô X
  mul_one : (prod.rightUnitor X).inv ‚â´ prod.map (ùüô X) one ‚â´ mul = ùüô X
  inv : X ‚ü∂ X
  inv_mul_cancel : prod.lift inv (ùüô X) ‚â´ mul = terminal.from X ‚â´ one
-- Note that we chose a minimal list of axioms. There should also be
-- a `mul_inv_cancel` for example, but it can be deduced from the
-- other axioms.

-- Category structure on group objects.
instance : Category (GroupObject C) where
  Hom G G' := {f : G.X ‚ü∂ G'.X | G.one ‚â´ f = G'.one ‚àß
    prod.map f f ‚â´ G'.mul = G.mul ‚â´ f}
  id G := ‚ü®ùüô G.X, by simp‚ü©
  comp f g := ‚ü®f.1 ‚â´ g.1, ‚ü®by rw [‚Üê assoc, f.2.1, g.2.1],
    by rw [‚Üê prod.map_map, assoc, g.2.2, ‚Üê assoc, f.2.2, assoc]‚ü©‚ü©
  id_comp f := by simp
  comp_id f := by simp
  assoc f g h := by simp

-- The forgetful functor from group objects in `C` to `C`.
def GroupObjectForget : GroupObject C ‚•§ C where
  obj G := G.X
  map f := f.1
  map_id _ := by rfl
  map_comp _ _ := by rfl

/-
Our next goal is to show that group objects in the category `Type u`
are just groups. (In fact, we will construct an equivalence of
categories `GroupObject (Type u) ‚âå Grp.{u}`.) To simplify the proofs,
we use some lemmas about products in `Type u`.
-/

section Lemmas

lemma prod.map_fst_apply {A B C D : Type u} (f : A ‚ü∂ B) (g : C ‚ü∂ D)
    (x : A ‚®Ø C) :
    prod.fst (X := B) (prod.map f g x) = f (prod.fst (X := A) x) := by
  change (prod.map f g ‚â´ prod.fst) _ = _
  rw [prod.map_fst]
  rfl

lemma prod.map_snd_apply {A B C D : Type u} (f : A ‚ü∂ B) (g : C ‚ü∂ D)
    (x : A ‚®Ø C) :
    prod.snd (X := B) (prod.map f g x) = g (prod.snd (X := A) x) := by
  change (prod.map f g ‚â´ prod.snd) _ = _
  rw [prod.map_snd]
  rfl

lemma prod.lift_fst_apply {A B C : Type u} (f : A ‚ü∂ B) (g : A ‚ü∂ C) (x : A) :
    prod.fst (X := B) (prod.lift f g x) = f x := by
  change (prod.lift f g ‚â´ prod.fst) _ = _
  rw [prod.lift_fst]

lemma prod.lift_snd_apply {A B C : Type u} (f : A ‚ü∂ B) (g : A ‚ü∂ C) (x : A) :
    prod.snd (X := B) (prod.lift f g x) = g x := by
  change (prod.lift f g ‚â´ prod.snd) _ = _
  rw [prod.lift_snd]

end Lemmas

/- If `G` is a group object in `Type u`, then `G.X` gets a canonical
`Group` instance. We define this instance bit by bit as usual: first
we introduce the `Mul`, `Inv` and `One` instances, then we prove
their properties.

For example, the `Mul` seems easy, because it is just a map
`G.X √ó G.X ‚Üí G.X`, i.e. a morphism `G.X √ó G.X ‚ü∂ G.X` in the category
`Type u`, and we have a morphism `G.X ‚®Ø G.X ‚ü∂ G.X` given by `G.mul`.
Similary, the unit element of `G.X` is the image of the unique
element of `‚ä§_ (Type u)` by `G.one`.

However, there is a technical complication: the categorical product
`G.X ‚®Ø G.X` in `Type u` (chosen by the axiom of choice among all binary
products) is NOT the Cartesian product `G.X √ó G.X`, it is only isomorphic
to it, etc.

(In fact, this shows that our definitio of group objects is not
the right one. There is a class called `ChosenFiniteProducts` which
allows us to choose "nice" representatives in a category such as
`Type u`, and we should use the finite products given by this class
to define group objects. I didn't do this because I wanted to torture
you a bit. End of digression.)
-/

#check Types.binaryProductIso

/- We will use that `‚ä§_ (Type u)` has a unique element, which
is called `default`.
It also has a `Subsingleton` insteance, so to prove that two of its
elements are equal, you can use this: -/
#check Subsingleton.elim
#print Subsingleton

noncomputable instance (G : GroupObject (Type u)) : Mul G.X where
  mul x y := sorry

noncomputable instance (G : GroupObject (Type u)) : One G.X where
  one := G.one default

noncomputable instance (G : GroupObject (Type u)) : Inv G.X where
  inv x := G.inv x

noncomputable instance (G : GroupObject (Type u)) : Group G.X where
  mul_assoc x y z := by
--    dsimp [HMul.hMul, Mul.mul]
    sorry
  one_mul x := by
    dsimp [HMul.hMul, Mul.mul, OfNat.ofNat, One.one]
    sorry
  mul_one x := by
    dsimp [HMul.hMul, Mul.mul, OfNat.ofNat, One.one]
    sorry
  inv_mul_cancel x := by
    dsimp [HMul.hMul, Mul.mul, Inv.inv]
    sorry

/-
If `f : G ‚ü∂ G'` is a morphism of group objects in `Type u`, this
constructs the corresponding morphism of groups from `G.X` to `G'.X`.
-/
@[simp]
def MonoidHomOfHom {G G' : GroupObject (Type u)} (f : G ‚ü∂ G') :
    G.X ‚Üí* G'.X where
      toFun := f.1
      map_one' := by
        change (G.one ‚â´ f.1) default = 1
        rw [f.2.1]
        rfl
      map_mul' x y := by
        dsimp [HMul.hMul, Mul.mul]
        sorry

-- The functor from `GroupObject (Type u)` to `Grp.{u}` sending
-- `G` to `G.X` with the group structure defined above.
noncomputable def CanIso : GroupObject (Type u) ‚•§ Grp.{u} where
  obj G := Grp.of G.X
  map f := Grp.ofHom (MonoidHomOfHom f)
  map_id X := by sorry
  map_comp f g := by sorry

/- The functor in the other direction: if `G` is a group, then
it defines a group object in `Type u`, because we have a morphism
`G √ó G ‚Üí G` defined by the multiplication, etc. However, there is
the same technical complication as before: the categorical product
`G ‚®Ø G` in `Type u` is NOT the Cartesian product `G √ó G`, it is only
isomorphic to it, etc.
-/
#check Types.binaryProductIso

-- Every group defines a group object in `Type u`.
@[simp]
noncomputable def GroupObjectOfGrp (G : Type u) [Group G] :
    GroupObject (Type u) where
  X := G
  mul p := sorry
  mul_assoc := by
    dsimp
    sorry
  one := sorry
  one_mul := by
    sorry
  mul_one := by
    dsimp
    sorry
  inv x := x‚Åª¬π
  inv_mul_cancel := by
    ext
    sorry

-- Every morphism of groups defines a morphism of group objects.
@[simp]
noncomputable def HomOfMonoidHom {G G' : Type u} [Group G] [Group G']
    (f : G ‚Üí* G') : GroupObjectOfGrp G ‚ü∂ GroupObjectOfGrp G' where
      val := f.toFun
      property := by
        constructor
        ¬∑ sorry
        ¬∑ sorry

-- Putting all this together into a functor.
noncomputable def CanIsoInv : Grp.{u} ‚•§ GroupObject (Type u) where
  obj G := GroupObjectOfGrp G.1
  map f := HomOfMonoidHom f
  map_id G := by
    dsimp
    rw [‚Üê SetCoe.ext_iff]
    ext
    rfl
  map_comp f g := by
    dsimp
    rw [‚Üê SetCoe.ext_iff]
    ext
    rfl

/-
To get an equivalence, we now need to define isomorphisms of functors
`ùü≠ (GroupObject (Type u)) ‚âÖ CanIso ‚ãô CanIsoInv` and
`CanIsoInv ‚ãô CanIso ‚âÖ ùü≠ Grp.{u}`. These morphisms are basically given
by the identity, but we do have to check the compatibility with the
various group/group object structures.
-/

noncomputable def CanUnit : ùü≠ (GroupObject (Type u)) ‚âÖ CanIso ‚ãô CanIsoInv := by
  refine NatIso.ofComponents (fun G ‚Ü¶ ?_) (fun f ‚Ü¶ ?_)
  ¬∑ dsimp [CanIso, CanIsoInv]
    refine {hom := ?_, inv := ?_, hom_inv_id := ?_, inv_hom_id := ?_}
    ¬∑ refine {val := ùüô G.X, property := ‚ü®?_, ?_‚ü©}
      ¬∑ dsimp
        ext
        sorry
      ¬∑ dsimp
        ext
        sorry
    ¬∑ sorry
    ¬∑ sorry
    ¬∑ sorry
  ¬∑ dsimp [CanIso, CanIsoInv]
    rw [‚Üê SetCoe.ext_iff]
    sorry

noncomputable def CanCounit : CanIsoInv ‚ãô CanIso ‚âÖ ùü≠ Grp.{u} := by
  refine NatIso.ofComponents (fun G ‚Ü¶ ?_) (fun f ‚Ü¶ ?_)
  ¬∑ dsimp [CanIsoInv, CanIso]
    refine MulEquiv.toGrpIso ?_
    refine {toFun := fun x ‚Ü¶ x, invFun := fun x ‚Ü¶ x, left_inv := fun _ ‚Ü¶ by rfl,
            right_inv := fun _ ‚Ü¶ by rfl,  map_mul' := fun x y ‚Ü¶ ?_}
    sorry
  ¬∑ ext
    rfl

-- The equivalence.
noncomputable def CanEquiv : GroupObject (Type u) ‚âå Grp.{u} where
  functor := CanIso
  inverse := CanIsoInv
  unitIso := CanUnit
  counitIso := CanCounit
  functor_unitIso_comp _ := by sorry

-- Compatibility of the equivalence with the forgetful functors.
noncomputable def CanEquivCompat :
    CanEquiv.functor ‚ãô forget Grp ‚âÖ GroupObjectForget (Type u) :=
  NatIso.ofComponents (fun _ ‚Ü¶ Iso.refl _) (fun _ ‚Ü¶ rfl)

end GroupObject
