import Mathlib.CategoryTheory.Category.Cat
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Assoc
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Iso
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete

/-
## Bicategories

A bicategory is, informally, a category`B` where the `Hom`
types have themselves category structures. This means that,
given `X, Y : B` and `f, g : X âŸ¶ Y`, there is a notion of
morphism from `f` to `g`, which we call a 2-morphism
(and we refer to `f` and `g` as 1-morphisms).

A bit less informally, a bicategory is the data of a type
`B` and, for all `X, Y : B`, of a category `Hom X Y` whose
objects are the 1-morphisms from `X` to `Y`. We
also want identity 1-morphisms `id X : Hom X X` and composition
*functors* `Hom X Y Ã— Hom Y Z â¥¤ Hom X Z`.
Associativity of composition and the unit property of the
identities objects are not strictly true but are expressed by
isomorphisms called `associator`, `leftUnitor` and
`rightUnitor`, and there are two axioms called the
`triangle` and `pentagon`. The triangle axiom expresses
a compatibility between the associator and the left/right
unitors, and the pentagon axiom is a compatibility property
of associators.

More precisely, the triangle axiom says that, for
all `X, Y, Z : B` and for all `f : X âŸ¶ Y` and `g : Y âŸ¶ Z`,
the two possible 2-isomorphisms from `(f â‰« ğŸ™ Y) â‰« g` to
`f â‰« (ğŸ™ Y â‰« g)` (one the associator, and the other
formed by composing right and left unitors) are equal.
The pentagon axiom says that, given `X, Y, Z, T, U : B`
and 1-morphisms `f : X âŸ¶ Y`, `g : Y âŸ¶ Z`, `h : Z âŸ¶ T`
and `i : T âŸ¶ U`, the two possible 2-isomorphisms
from `((f â‰« g) â‰« h) â‰« i` to `f â‰« (g â‰« (h â‰« i))`
(formed by applying associators in different ways) are
equal.

To get even more technical, composition of 1-morphisms in
mathlib is not actually given as a functor but through the
whiskering functions, though the functor is available
later as `CategoryTheory.Bicategory.precomposing` or
`CategoryTheory.Bicategory.postcomposing`.

As another technical note, a bicategory can have three
distinct universe levels: one for the objects, one for
the 1-morphisms, and one for the 2-morphisms.
-/

universe u v w

open CategoryTheory Category
open scoped Bicategory

#print Bicategory
/-
@[nolint checkUnivs]
class Bicategory (B : Type u) extends CategoryStruct.{v} B where
  -- remember that `CategoryStruct` is the data of the `Hom`
  -- types, the composition and the identities, but without
  -- the axioms
  -- category structure on the collection of 1-morphisms:
  homCategory : âˆ€ a b : B, Category.{w} (a âŸ¶ b) := by infer_instance
  -- left whiskering:
  whiskerLeft {a b c : B} (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h) :
      f â‰« g âŸ¶ f â‰« h
  -- right whiskering:
  whiskerRight {a b c : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c) :
      f â‰« h âŸ¶ g â‰« h
  -- associator:
  associator {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
      (f â‰« g) â‰« h â‰… f â‰« g â‰« h
  -- left unitor:
  leftUnitor {a b : B} (f : a âŸ¶ b) : ğŸ™ a â‰« f â‰… f
  -- right unitor:
  rightUnitor {a b : B} (f : a âŸ¶ b) : f â‰« ğŸ™ b â‰… f
  -- axioms for left whiskering:
  whiskerLeft_id : âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c),
      whiskerLeft f (ğŸ™ g) = ğŸ™ (f â‰« g) := by
    aesop_cat
  whiskerLeft_comp :
    âˆ€ {a b c} (f : a âŸ¶ b) {g h i : b âŸ¶ c} (Î· : g âŸ¶ h) (Î¸ : h âŸ¶ i),
      whiskerLeft f (Î· â‰« Î¸) = whiskerLeft f Î· â‰« whiskerLeft f Î¸ := by
    aesop_cat
  id_whiskerLeft :
    âˆ€ {a b} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
      whiskerLeft (ğŸ™ a) Î· =
        (leftUnitor f).hom â‰« Î· â‰« (leftUnitor g).inv := by
    aesop_cat
  comp_whiskerLeft :
    âˆ€ {a b c d} (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h'),
      whiskerLeft (f â‰« g) Î· =
        (associator f g h).hom â‰«
        whiskerLeft f (whiskerLeft g Î·) â‰«
        (associator f g h').inv := by
    aesop_cat
  -- axioms for right whiskering:
  id_whiskerRight : âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c),
      whiskerRight (ğŸ™ f) g = ğŸ™ (f â‰« g) := by
    aesop_cat
  comp_whiskerRight :
    âˆ€ {a b c} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h) (i : b âŸ¶ c),
      whiskerRight (Î· â‰« Î¸) i = whiskerRight Î· i â‰« whiskerRight Î¸ i := by
    aesop_cat
  whiskerRight_id :
    âˆ€ {a b} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
      whiskerRight Î· (ğŸ™ b) = (rightUnitor f).hom â‰« Î· â‰«
        (rightUnitor g).inv := by
    aesop_cat
  whiskerRight_comp :
    âˆ€ {a b c d} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d),
      whiskerRight Î· (g â‰« h) =
        (associator f g h).inv â‰«
          whiskerRight (whiskerRight Î· g) h â‰«
            (associator f' g h).hom := by
    aesop_cat
  -- associativity of whiskerings:
  whisker_assoc :
    âˆ€ {a b c d} (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d),
      whiskerRight (whiskerLeft f Î·) h =
        (associator f g h).hom â‰«
        whiskerLeft f (whiskerRight Î· h) â‰« (associator f g' h).inv := by
    aesop_cat
  -- exchange law of left and right whiskerings:
  whisker_exchange :
    âˆ€ {a b c} {f g : a âŸ¶ b} {h i : b âŸ¶ c} (Î· : f âŸ¶ g) (Î¸ : h âŸ¶ i),
      whiskerLeft f Î¸ â‰« whiskerRight Î· i = whiskerRight Î· h â‰«
      whiskerLeft g Î¸ := by
    aesop_cat
  -- pentagon identity:
  pentagon :
    âˆ€ {a b c d e} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e),
      whiskerRight (associator f g h).hom i â‰«
          (associator f (g â‰« h) i).hom â‰«
          whiskerLeft f (associator g h i).hom =
          (associator (f â‰« g) h i).hom â‰«
          (associator f g (h â‰« i)).hom := by
    aesop_cat
  -- triangle identity:
  triangle :
    âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c),
      (associator f (ğŸ™ b) g).hom â‰«
      whiskerLeft f (leftUnitor g).hom
      = whiskerRight (rightUnitor f).hom g := by
    aesop_cat
-/

--This of course unreadable.

/-
Notation:
- For `X, Y : B`, the category `Hom X Y` is written `X âŸ¶ Y`.
- For `X : B`, the identity in `X âŸ¶ X` is written `ğŸ™ X`.
- Composition of 1-morphisms is denoted by `â‰«` and written
in diagrammatic order.
- We also use `âŸ¶`, `ğŸ™` and `â‰«` to denote 2-morphisms,
and the identities and compositions inside the category
`X âŸ¶ Y`, so be careful.
- The left unitor of a 1-morphism `f` is denoted by
`Î»_ f`.
- The right unitor of a 1-morphism `f` is denoted by
`Ï_ f`.
- The associator of three 1-morphisms `f, g, h` is denoted
by `Î±_ f g h`.
- We use `â—` (\ + lhd) for left whiskering.
- We use `â–·` (\ + rhd) for right whiskering.

(The last five are scoped notation, to make this available
you need to `open` or `open scoped` the `Bicategory`
namespace.)
(Check if they know the difference between `open` and
`open scoped`.)
-/

variable (B : Type*) [Bicategory B] (X Y Z T U : B)
  (f f' f'' : X âŸ¶ Y) (g g' : Y âŸ¶ Z) (h : Z âŸ¶ T) (i : T âŸ¶ U)
  (u : f âŸ¶ f') (u' : f' âŸ¶ f'') (v : g âŸ¶ g')

#check ğŸ™ X
#check ğŸ™ f
#check f â‰« g
#check u â‰« u'
#check f â— v
#check u â–· g
#check Î»_ f
#check Ï_ f
#check Î±_ f g h

#check Bicategory.triangle f
#check Bicategory.pentagon f g h i

#check Bicategory.precomposing X Y Z
#check Bicategory.postcomposing X Y Z

/-
# Examples.

The first example is the bicategory `Cat` of categories at
fixed universe levels.
-/
#check Cat

#synth Bicategory Cat.{v, u}

#check Cat.bicategory

#print Cat.bicategory

/-
Note that `Cat` is what is called a *strict* bicategory, i.e.
a bicategory where the associators, left unitors and right
unitors isomorphisms are just identity morphisms.

In mathlib, as the type of 2-morphisms (where associators etc
live) depends on the 1-morphisms, we cannot use that
definition immediately, so instead we use the construction
`eqToIso` (the isomorphism version of `eqToHom`).
-/

#check eqToIso -- from an equality between objects, produces
               -- an isomorphism between them

#print Bicategory.Strict

/-
class Bicategory.Strict : Prop where
  /-- Identity morphisms are left identities for composition. -/
  id_comp : âˆ€ {a b : B} (f : a âŸ¶ b), ğŸ™ a â‰« f = f := by aesop_cat
  /-- Identity morphisms are right identities for composition. -/
  comp_id : âˆ€ {a b : B} (f : a âŸ¶ b), f â‰« ğŸ™ b = f := by aesop_cat
  /-- Composition in a bicategory is associative. -/
  assoc : âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
      (f â‰« g) â‰« h = f â‰« g â‰« h := by
    aesop_cat
  /-- The left unitors are given by equalities -/
  leftUnitor_eqToIso : âˆ€ {a b : B} (f : a âŸ¶ b),
      Î»_ f = eqToIso (id_comp f) := by aesop_cat
  /-- The right unitors are given by equalities -/
  rightUnitor_eqToIso : âˆ€ {a b : B} (f : a âŸ¶ b),
      Ï_ f = eqToIso (comp_id f) := by aesop_cat
  /-- The associators are given by equalities -/
  associator_eqToIso :
    âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
       Î±_ f g h = eqToIso (assoc f g h) := by
    aesop_cat
-/

/-
Another example are monoidal categories. A monoidal
category is usually presented as a category with an
extra operation on objects, often denoted by `âŠ—`, which
also acts on morphisms and satisfies some compatibility
conditions (associativity up to isomorphism, existence
of units up to isomorphism...). Think of vector spaces with
the tensor product, or types with the Cartesian product.

But we can also see a monoidal category as a (non-strict)
bicategory with only one object `X`. The monoical category
is then `X âŸ¶ X`, and composition of 1-morphisms gives the
extra operation `âŠ—`. The axioms are identical. See
`Mathlib.CategoryTheory.Bicategory.SingleObj`.
-/

/-
Another example is the fundamental 2-groupoid of a topological
space `X`:
- The objects are points of `X`;
- The 1-morphisms `x âŸ¶ y` are paths from `x` to `y`, i.e.
continuous maps `[0,1] â†’ X` sending `0` to `x` and `1` to `y`.
- Composition of 1-morphisms is given by concatenation, and
the identities are constant paths.
- The 2-morphisms between two paths are homotopies between
the paths (fixing the end points).
-/

/-
Another example is the bicategory of spans in a category
`C` (admitting fiber products). It is not strict in general.

We will construct this example.
-/

open Limits

variable {C : Type u} [Category.{v} C]

-- The objects are objects of `C`.
variable (C) in
structure Span where
  pt : C
-- Even though `C` and `Span C` have the same objects, we
-- don't want to give them the same name, because the
-- category instance on `C` would clash the bicategory
-- instance that we will construct. So we introduce `Span C`
-- as a structure whose unique field is a term of type `C`.
-- (We could also have used a type synonym, i.e.
-- `def Span (C : Type*) := C`.)

-- Given `X, Y : C`, the 1-morphisms from `X` to `Y` are
-- spans from `X` to `Y`, i.e. diagrams
--       Z
--   f /  \ g  with f : Z âŸ¶ X and g : Z âŸ¶ Y
--   X     Y
structure OneMor (X Y : Span C) where
  roof : C
  left : roof âŸ¶ X.pt
  right : roof âŸ¶ Y.pt

instance : Quiver (Span C) where
  Hom := OneMor

-- The identity 1-morphism from `X` to `X` is given by
-- taking `Z` equal to `X` and `f, g` equal to `ğŸ™ X`.
@[simp]
def OneMorId (X : Span C) : OneMor X X where
  roof := X.pt
  left := ğŸ™ _
  right := ğŸ™ _

section OneMorComp

variable [HasPullbacks C]

-- The composition of 1-morphisms is given by the following
-- diagram, that I will also draw on the board.

--      Z
--     / \            Here we take `Z` equal to the
--   Y    Y'          fiber product of `Y` and `Y'` over
--  / \f g/ \          `X'` (called `pullback f g` in mathlib).
-- X    X'   X''      The two projections give morphisms from
--                    `Z` to `Y` and `Y'`, and we compose them
--                    with the available morphisms to get
--                    morphisms `Z âŸ¶ X` and `Z âŸ¶ X''`.

#check pullback
#check pullback.fst
#check pullback.snd

@[simp]
noncomputable def OneMorComp {X Y Z : Span C}
    (a : OneMor X Y) (b : OneMor Y Z) : OneMor X Z where
      roof := pullback a.right b.left
      left := pullback.fst _ _ â‰« a.left
      right := pullback.snd _ _ â‰« b.right

-- With this, we get the `CategoryStruct` on `Span C`:
@[simp]
noncomputable instance : CategoryStruct (Span C) where
  id := OneMorId
  comp a b := OneMorComp a b

end OneMorComp


-- The 2-morphisms are commutative diagrams between spans.
@[ext]
structure TwoMor {X Y : Span C} (a b : OneMor X Y) where
  hom_roof : a.roof âŸ¶ b.roof
  left_comp : hom_roof â‰« b.left = a.left := by aesop_cat
  right_comp : hom_roof â‰« b.right = a.right := by aesop_cat

@[simp, reassoc]
lemma TwoMor.left_comp' {X Y : Span C} (a b : OneMor X Y)
    (u : TwoMor a b) : u.hom_roof â‰« b.left = a.left :=
  u.left_comp

@[simp, reassoc]
lemma TwoMor.right_comp' {X Y : Span C} (a b : OneMor X Y)
    (u : TwoMor a b) : u.hom_roof â‰« b.right = a.right :=
  u.right_comp

-- Let's make `OneMor X Y` into a category:
instance {X Y : Span C} : Category (X âŸ¶ Y) where
  Hom a b := TwoMor a b
  id a := {hom_roof := ğŸ™ a.roof}
  comp {a b c} u v := {hom_roof := u.hom_roof â‰« v.hom_roof}
-- Note how a lot of the fields are filled by their default
-- value `by aesop_cat`.

@[simp]
lemma TwoMor.id {X Y : Span C} {f : X âŸ¶ Y} :
    (ğŸ™ f : TwoMor f f).hom_roof = ğŸ™ f.roof := rfl

@[simp]
lemma TwoMor.comp {X Y : Span C} {f g h : X âŸ¶ Y} (u : f âŸ¶ g)
    (v : g âŸ¶ h) :
    (u â‰« v).hom_roof = u.hom_roof â‰« v.hom_roof := rfl

-- The two lemmas above look useless, but you can check
-- what happens in `TwoMor.isoMk` if you comment them.
-- Suddenly `simp` cannot solve obvious goals anymore.


@[ext (iff := false)]
lemma TwoMor.ext' {X Y : Span C} {f g : X âŸ¶ Y}
    {u v : (f : OneMor X Y) âŸ¶ g} (eq : u.hom_roof = v.hom_roof) : u = v :=
  TwoMor.ext eq
-- This allows us to use the `ext` tactic on 2-morphisms.
-- If you remove it, `aesop_cat` loses a lot of its power.

@[simp]
def TwoMor.homMk {X Y : Span C} {a b : X âŸ¶ Y}
    (u : a.roof âŸ¶ b.roof) (compâ‚ : u â‰« b.left = a.left)
    (compâ‚‚ : u â‰« b.right = a.right) : a âŸ¶ b where
      hom_roof := u
      left_comp := compâ‚
      right_comp := compâ‚‚

#check cancel_epi
#check eqToIso

@[simp]
def TwoMor.isoMk {X Y : Span C} {a b : X âŸ¶ Y}
    (u : a.roof â‰… b.roof) (compâ‚ : u.hom â‰« b.left = a.left)
    (compâ‚‚ : u.hom â‰« b.right = a.right) : a â‰… b where
      hom := TwoMor.homMk u.hom compâ‚ compâ‚‚
      inv := by
        refine TwoMor.homMk u.inv ?_ ?_
        Â· rw [â† cancel_epi u.hom]
          aesop_cat
        Â· rw [â† cancel_epi u.hom]
          aesop_cat
-- Hint: `cancel_epi`.

variable [HasPullbacks C]

-- Now for the bicategory structure on `Span C`:
noncomputable instance : Bicategory (Span C) where
  homCategory X Y := inferInstance
  whiskerLeft {X X' X''} a {b b'} u :=
    -- draw the diagram! (see below for useful functions)
    {
      hom_roof := pullback.lift (pullback.fst _ _)
        (pullback.snd _ _ â‰« u.hom_roof)
        (by simp [pullback.condition])
    }
  whiskerRight {X X' X''} {a a'} u b :=
    {
      hom_roof := pullback.lift (pullback.fst _ _ â‰« u.hom_roof)
        (pullback.snd _ _) (by simp [pullback.condition])
    }
  associator {X X' X'' X'''} a b c := by
    refine TwoMor.isoMk (pullbackAssoc _ _ _ _) ?_ ?_
    Â· sorry
    Â· sorry
  leftUnitor {X X'} a := by
    refine TwoMor.isoMk ?_ ?_ ?_
    Â· exact asIso (pullback.snd (ğŸ™ X.pt) a.left)
-- Here we use the fact that mathlib knows that the pullback
-- of an isomorphism is an isomorphism (and the identity is
-- an isomorphism).
-- If a morphism `Î±` has an `IsIso` instance, then
-- `asIso Î±` is `Î±` upgraded to an isomorphism.
    Â· sorry
    Â· sorry
  rightUnitor {X X'} a := by
    refine TwoMor.isoMk ?_ ?_ ?_
    Â· exact asIso (pullback.fst a.right (ğŸ™ X'.pt))
    Â· sorry
    Â· sorry
  whiskerLeft_id := by
    intros a b c f g
    ext
    dsimp
    refine pullback.hom_ext ?_ ?_
    rw [pullback.lift_fst]
    have := id_comp (pullback.fst f.right g.left)
    rw [this]
    simp only [comp_id, limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app, id_comp]
  whiskerLeft_comp := sorry
  id_whiskerLeft := by
    intros a b c f g
    ext
    dsimp
    refine pullback.hom_ext ?_ ?_
    rw [pullback.lift_fst]
    sorry
    sorry
  comp_whiskerLeft := sorry
  id_whiskerRight := sorry
  comp_whiskerRight := sorry
  whiskerRight_id := sorry
  whiskerRight_comp := sorry
  whisker_assoc := sorry
  whisker_exchange := sorry
  pentagon := by
    intros a b c d e f g h i
    dsimp
    sorry
  triangle := sorry

-- We need the universal property of the pullback.
#check pullback.fst
#check pullback.snd
#check pullback.condition
#check pullback.lift
#check pullback.lift_fst
#check pullback.lift_snd
#check pullback.hom_ext
#check pullback.map

-- Associativity of pullbacks.
#check pullbackAssoc

/-
# Functors

A pseudofunctor `F` between bicategories `B` and `B'` is
a map `F.obj` between objects, a map `F.map` between
1-morphisms (respecting source and target) and a map
`F.mapâ‚‚` between 2-morphisms (respecting source and target),
such that `F.mapâ‚‚` is compatible with identities and composition,
and `F.map` is compatible with identities and composition *up
to isomorphism* (+ compatibilities with whiskering, associators
and left/right unitors).
-/
#print Pseudofunctor

/-
structure Pseudofunctor (B : Type uâ‚) [Bicategory.{wâ‚, vâ‚} B] (C : Type uâ‚‚)
    [Bicategory.{wâ‚‚, vâ‚‚} C] extends PrelaxFunctor B C where
  obj : B â†’ C
  map {X Y : B} : (X âŸ¶ Y) â†’ (obj X âŸ¶ obj Y)
  mapâ‚‚ {a b : B} {f g : a âŸ¶ b} : (f âŸ¶ g) â†’ (map f âŸ¶ map g)
  mapâ‚‚_id {a b : B} (f : a âŸ¶ b) : mapâ‚‚ (ğŸ™ f) = ğŸ™ (map f)
  mapâ‚‚_comp {a b : B} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h) :
    mapâ‚‚ (Î· â‰« Î¸) = (mapâ‚‚ Î·) â‰« (mapâ‚‚ Î¸)
-- The first five fields are the `PrelaxFunctor` structure.
  mapId (a : B) : map (ğŸ™ a) â‰… ğŸ™ (obj a)
  mapComp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
      map (f â‰« g) â‰… map f â‰« map g
  mapâ‚‚_whisker_left :
    âˆ€ {a b c : B} (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h),
      mapâ‚‚ (f â— Î·) = (mapComp f g).hom â‰« map f â— mapâ‚‚ Î· â‰«
      (mapComp f h).inv := by
    aesop_cat
  mapâ‚‚_whisker_right :
    âˆ€ {a b c : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c),
      mapâ‚‚ (Î· â–· h) = (mapComp f h).hom â‰« mapâ‚‚ Î· â–· map h â‰«
      (mapComp g h).inv := by
    aesop_cat
  mapâ‚‚_associator :
    âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
      mapâ‚‚ (Î±_ f g h).hom = (mapComp (f â‰« g) h).hom â‰«
      (mapComp f g).hom â–· map h â‰«
      (Î±_ (map f) (map g) (map h)).hom â‰« map f â— (mapComp g h).inv â‰«
      (mapComp f (g â‰« h)).inv := by
    aesop_cat
  mapâ‚‚_left_unitor :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      mapâ‚‚ (Î»_ f).hom = (mapComp (ğŸ™ a) f).hom â‰«
      (mapId a).hom â–· map f â‰« (Î»_ (map f)).hom := by
    aesop_cat
  mapâ‚‚_right_unitor :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      mapâ‚‚ (Ï_ f).hom = (mapComp f (ğŸ™ b)).hom â‰«
      map f â— (mapId b).hom â‰« (Ï_ (map f)).hom := by
    aesop_cat
-/

/-
As an example, we can define a pseudofunctor from `C` to
`Span C`. Note that a 1-category can be seen as a
strict bicategory where the only 2-morphisms are identity
morphisms. To avoid instance clashes, this is called
`LocallyDiscrete C`.
-/

#synth Bicategory (LocallyDiscrete C)

#print LocallyDiscrete
-- This is again a structure with a unique field `as`.
-- So if `X : LocallyDiscrete C`, the corresponding object of
-- `C` is called `X.as`.
-- Note that if `f : X âŸ¶ Y` is a 1-morphisms in `LocallyDiscrete C`,
-- the corresponding morphisms of `C` is called `f.as`.

/-
The pseudofunctor from `C` to `Span C` will be the identity on
objects. It will send `f : X âŸ¶ Y` to the span
   X
 ğŸ™/ \f
X    Y
As the only 2-morphisms in `C` are identity morphisms, it will
send them to identity morphisms. In fact we will need to use
`eqToHom`, and the following function (saying that if there
is a 2-morphism between two 1-morphisms `f` and `g` in a locally
discrete bicategory, then `f` and `g` are equal):
-/
#check LocallyDiscrete.eq_of_hom

@[simp]
lemma eqToHom_hom_roof {X Y : Span C} {a b : X âŸ¶ Y} (eq : a = b) :
    (eqToHom eq).hom_roof = eqToHom (by rw [eq]) := by
  aesop_cat

noncomputable def ToSpan :
    Pseudofunctor (LocallyDiscrete C) (Span C) where
  obj X := {pt := X.as}
  map {X Y} f := {roof := X.as, left := ğŸ™ X.as, right := f.as}
  mapâ‚‚ {X Y f g} u := by
    have eq := LocallyDiscrete.eq_of_hom u
    refine eqToHom ?_
    simp [eq]
  mapâ‚‚_id := by simp
  mapâ‚‚_comp := by simp
  mapId X := by
    refine TwoMor.isoMk (Iso.refl _) ?_ ?_ <;> simp <;> rfl
  mapComp {X Y Z} f g := by
    refine TwoMor.isoMk ?_ ?_ ?_
    Â· exact (asIso (pullback.fst f.as (ğŸ™ _))).symm
    Â· simp only [LocallyDiscrete.comp_as, Iso.symm_hom, asIso_inv,
        IsIso.inv_comp_eq, comp_id]
      change pullback.fst _ _ â‰« _ = _
      simp
    Â· simp only [LocallyDiscrete.comp_as, Iso.symm_hom, asIso_inv,
        IsIso.inv_comp_eq]
      change pullback.snd _ _ â‰« _ = _
      dsimp
      rw [â† assoc, pullback.condition]
      simp
  mapâ‚‚_whisker_left := by
    intros a b c f g h Î·
    dsimp
    ext
    simp only [eqToHom_hom_roof, eqToHom_refl, Bicategory.whiskerLeft_eqToHom, TwoMor.comp, id_comp,
      IsIso.inv_hom_id]
  mapâ‚‚_whisker_right := by
    intros a b c f g h Î·
    dsimp
    ext
    simp
    sorry
  mapâ‚‚_associator := sorry
  mapâ‚‚_left_unitor := sorry
  mapâ‚‚_right_unitor := sorry
