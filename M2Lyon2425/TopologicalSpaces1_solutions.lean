import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.MeasureTheory.OuterMeasure.AE
import Mathlib.MeasureTheory.Measure.Lebesgue.Basic
import Mathlib.MeasureTheory.Measure.MeasureSpaceDef

open Filter Set Topology

/- Filters. -/

-- A filter `F` on a type `Î±` is set in `Set Î±` (i.e. a set of
-- sets in `Î±`) such that:
-- (1) The biggest set `âŠ¤` is in `F`;
-- (2) If `x,y : Set Î±` and `x âŠ† y`, then `x âˆˆ F` implies that `y âˆˆ F`;
-- (3) `F` is stable by finite intersections.

-- More precisely, `Filter` is a structure:
#print Filter

variable {Î± Î² : Type*}

/-- If `F` is a filter on `Î±`, and `U` a subset of `Î±` then we can
write `U âˆˆ F` as on paper, thanks to the following declaration: -/
--instance instMembership : Membership (Set Î±) (Filter Î±) :=
--  âŸ¨fun U F => U âˆˆ F.setsâŸ©
-- NB: comment this, this is already declare in mathlib.

-- Examples:

-- If `a : Î±`, the set of sets containing `a` is a filter,
-- (and even an ultrafilter).
example (a : Î±) : Filter Î± where
  sets := {A | a âˆˆ A}
  univ_sets := mem_univ a
  sets_of_superset h h' := h' h
  inter_sets h h' := mem_inter h h'

-- More generally, if `s : Set Î±`, the set of sets containing `s`
-- is a filter.
example (s : Set Î±) : Filter Î± where
  sets := {A : Set Î± | s âŠ† A}
  univ_sets := subset_univ s
  sets_of_superset h h' := subset_trans h h'
  inter_sets h h' := subset_inter h h'

-- This is called a principal filter, `Filter.principal` in mathlib:
#print Filter.principal

-- The set of sets of natural integers (or real numbers, or
-- rational numbers...) that are "big enough" is a filter.
example : Filter â„• where
  sets := {A | âˆƒ n, Set.Ici n âŠ† A}
  univ_sets := by
    use 0, subset_univ _
  sets_of_superset h h' := by
    obtain âŸ¨n, hnâŸ© := h
    use n, fun _ hx â†¦ (subset_trans hn h') hx
  inter_sets h h' := by
    obtain âŸ¨n, hnâŸ© := h
    obtain âŸ¨m, hmâŸ© := h'
    use max n m
    erw [â† Ici_inter_Ici]
    exact fun _ hx â†¦ (inter_subset_inter hn hm) hx

-- Set.Ici a = [a, + âˆ)
-- Set.Ioo a b a b = (a, b)
-- Set.Ixx where x is o, c or i; o = open, c = closed, i = infinite
-- Set.Iio a = (- âˆ, a)

-- This filter is called `Filter.atTop`:
#print Filter.atTop
#check atTop
#print Filter.mem_atTop

-- There is also a filter for "small enough" elements, called
-- `Filter.atBot`.


-- The neighborhoods of a point in `â„` (or any metric or more
-- generally topological space):
example (a : â„) : Filter â„ where
  sets := {A | âˆƒ Îµ > 0, Set.Ioo (a - Îµ) (a + Îµ) âŠ† A}
  univ_sets := by
    use 1, zero_lt_one, subset_univ _
  sets_of_superset h h' := by
    obtain âŸ¨Îµ, hpos, hâŸ© := h
    use Îµ, hpos, subset_trans h h'
  inter_sets h h' := by
    obtain âŸ¨Îµ, hpos, hâŸ© := h
    obtain âŸ¨Îµ', hpos', h'âŸ© := h'
    use min Îµ Îµ', lt_min_iff.mpr âŸ¨hpos, hpos'âŸ©
    intro z hz
    apply (inter_subset_inter h h')
    rw [Ioo_inter_Ioo]
    change z âˆˆ Ioo (max (a - Îµ) (a - Îµ')) (min (a + Îµ) (a + Îµ'))
    rw [max_sub_sub_left a Îµ Îµ', min_add_add_left a Îµ Îµ']
    exact hz

example (a : â„) : Filter â„ where
  sets := {A | âˆƒ (U : Set â„), IsOpen U âˆ§ a âˆˆ U âˆ§ U âŠ† A}
  univ_sets := by
    use univ, isOpen_univ, mem_univ a, subset_refl _
  sets_of_superset h h' := by
    obtain âŸ¨U, hopen, ha, hsubâŸ© := h
    use U, hopen, ha, subset_trans hsub h'
  inter_sets h h' := by
    obtain âŸ¨U, hopen, ha, hsubâŸ© := h
    obtain âŸ¨U', hopen', ha', hsub'âŸ© := h'
    use U âŠ“ U', IsOpen.inter hopen hopen', mem_inter ha ha', inter_subset_inter hsub hsub'

-- This filter is called `nhs` or `ğ“` (\ + nhds):
#print nhds

-- If `a : â„`, we can also look at the set of subsets of `â„`
-- that contain an interval `(a-Îµ,a]` with `Îµ > 0`, and this is
-- still a filter.
def nhds_left (a : â„) : Filter â„ where
  sets := {A | âˆƒ Îµ > 0, Ioc (a - Îµ) a âŠ† A}
  univ_sets := by
    use 1, zero_lt_one, subset_univ _
  sets_of_superset h h' := by
    obtain âŸ¨Îµ, hpos, hâŸ© := h
    use Îµ, hpos, subset_trans h h'
  inter_sets h h' := by
    obtain âŸ¨Îµ, hpos, hâŸ© := h
    obtain âŸ¨Îµ', hpos', h'âŸ© := h'
    use min Îµ Îµ', lt_min_iff.mpr âŸ¨hpos, hpos'âŸ©
    intro z hz
    apply (inter_subset_inter h h')
    rw [Ioc_inter_Ioc]
    change z âˆˆ Ioc (max (a - Îµ) (a - Îµ')) (min a a)
    rw [max_sub_sub_left a Îµ Îµ', min_self]
    exact hz

/- If `Î±` has a measure `Î¼`, then we have a filter
`MesureTheory.ae Î¼` whose elements are co-null sets (i.e.
measurable sets whose complement has measure zero). This
is "the set of almost all elements of `Î±`".
-/
#print MeasureTheory.ae -- ae = almost everywhere

open MeasureTheory in
#check ae (volume : Measure â„)


/- Why filters?-/

/- Filters are (among other things) a very convenient way
to talk about convergence.

For example, consider a function `f : â„ â†’ â„` and `a,b : â„`.

Suppose that we want to say that the limit of `f` at `a`
is `b`. This means that, for every `Îµ > 0`, there exists
`Î´ > 0` such that `f` sends `(a - Î´, a + Î´)` to
`(b - Îµ, b + Îµ)`.
We can reformulate this by saying that `f â»Â¹' (b - Îµ, b + Îµ)`
is in the filter of neighborhoods of `a` for every `Îµ`, which
means: `âˆ€ (U : nhds b), f â»Â¹' U âˆˆ nhds a`.

But now suppose to say that `f(x)` tends to `b` as `x` tends to
`a` on the left. This means that for every `Îµ > 0`, there exists
`Î´ > 0` such that `f` sends `(a - Î´, a]` to `(b - Îµ, b + Îµ)`.
With filters, this becomes: `âˆ€ (U : nhds b), f â»Â¹'U âˆˆ nhds_left a`.

We can similarly express things like "`f(x)` approaches `b`
on the right when `x` approaches `a` on the left", etc.

Now suppose that we want to say `f(x)` tends to `b` as `x`
tends to `+ âˆ`. This means that, for every `Îµ > 0`, there
exists `M : â„` such that `f` sends `[M, + âˆ)` to
`(b - Îµ, b + Îµ)`. Or, with filters:
`âˆ€ (U : nhds b), f â»Â¹' U âˆˆ Filter.atTop`.
We could similarly express the fact that `f(x)` approaches
`b` from the left as `x` tends to `+ âˆ`, by using `nhds_left b`
instead of `nhds`.
`âˆ€ (U : nhds_left b), f â»Â¹' U âˆˆ Filter.atTop`.


Similarly, if `u : â„• â†’ â„` is a sequence (here with real values,
but it could have values in any topological space), we can
express the fact that `u` converges to `b : â„` with filters:
`âˆ€ (U : nhds b), f â»Â¹' U âˆˆ Filter.atTop`. --i.e. f â»Â¹' U contains
-- an interval [n, +âˆ).

Note that all these definitions of convergence can be written
in exactly the same way once we decide to use filters. This is
already nice, but it starts being really powerful when we
want to prove theorems about limits.

For example, let `f,g : â„ â†’ â„` and let `a,b,c : â„`. We can
prove that, if `f(x)` tends to `b` as `x` tends to `a`
and `g(y)` tends to `c` as `y` tends to `b`, then `(g âˆ˜ f) (x)`
tends to `c` as `x` tends to `a`.
But then suppose that we want to use that, if `f(x)` tends to
`b` on the right as `x` tends to `a` on the left and `g(y)`
tends to `c` on the left as `y` tends to `b` on the right, then
`(g âˆ˜ f) (x)` tends to `c` on the left as `x` tends to `a` on
the left. On paper, we can just say that "the proof is similar",
but Lean won't accept that, so we would have to rewrite the
proof. Now think about all the different possibilities
(including limits at infinity, limits as `x` is only in a certain
subset etc), and ask yourselves if you really want to write the
resulting 3487 lemmas (conservative estimation).

If instead we can express everything with filters, then
we only need to prove each statement once.
-/

-- First attempt to define convergence: `f : X â†’ Y` is a
-- function, we have a filter `F` on `X`, a filter `G` on
-- `Y`, and we want to say `f` tends to `Y` along `X`.
-- We generalize the definition that appeared before.

def Tendstoâ‚ {X Y : Type*} (f : X â†’ Y) (F : Filter X)
    (G : Filter Y) := âˆ€ V âˆˆ G, f â»Â¹' V âˆˆ F

-- Compatibility with composition.
example {X Y Z : Type*} (f : X â†’ Y) (g : Y â†’ Z) (F : Filter X)
    (G : Filter Y) (H : Filter Z) :
    Tendstoâ‚ f F G â†’ Tendstoâ‚ g G H â†’ Tendstoâ‚ (g âˆ˜ f) F H := by
  intro h h' U hU
  rw [preimage_comp]
  apply h
  apply h'
  exact hU

#check Set.preimage_comp

/- An intuitive way to think about filters, and a reformulation
of convergence.

Remember that, for every `s : Set Î±`, we have the principal filter
generated by `s`: it is the filter whose elements are sets
containing `s`.

We think of this filter as standing in for `s`, and we think of
more general filters as "generalized sets" of `Î±`. With this
point of view, if `F` is a filter on `Î±` and `s : Set Î±`, saying
that `s âˆˆ F` means that `s` "contains" the corresponding
"generalized set".

For example, if `Î±` is `â„` (or more generally if `Î±` has a
topology) and `a : Î±`, then `ğ“ a` is "the set of elements
close enough to `a`".
If `Î±` has a (pre)order, then `Filter.atTop` is "the set
of elements that are big enough".
If `Î±` has a measure `Î¼`, then we have a filter
`MesureTheory.ae Î¼` whose elements are co-null sets (i.e.
measurable sets whose complement has measure zero). This
is "the set of almost all elements of `Î±`".

(If you know what this means, filters on `X` actually
correspond to closed sets of the Stone-Cech compactification
of the discrete space `X`. If you don't know what this means,
don't worry.)
-/

/- Now that we think of filters as generalized sets,
let's extend some set notions to them.

The first is the order relation: sets on `Î±` are
ordered by inclusion. How does it translate to filters?
Well, it means that every set that contains `t` also
contains `s`:
-/

example (s t : Set Î±) : s âŠ† t â†”
    (Filter.principal t).sets âŠ† (Filter.principal s).sets := by
  constructor
  Â· exact fun h _ hA â†¦ le_trans h hA
  Â· exact fun h â†¦ h (mem_principal_self t)

-- So this is how we define order on filters:
#print Filter.le_def  -- F â‰¤ G â†” âˆ€ x âˆˆ G, x âˆˆ F

example (F : Filter Î±) (s : Set Î±) :
    ğ“Ÿ s â‰¤ F â†” âˆ€ A âˆˆ F, s âŠ† A := by
  constructor
  Â· exact fun h _ hA â†¦ h hA
  Â· exact fun h A hA â†¦ h A hA

example (F : Filter Î±) (s : Set Î±) :
    F â‰¤ Filter.principal s â†” s âˆˆ F := by
  constructor
  Â· exact fun h â†¦ h (mem_principal_self s)
  Â· exact fun h _ hA â†¦ F.sets_of_superset h hA

/- The second notion is the image of a filter by
a function `f : Î± â†’ Î²`. This operation is called
`Filter.map`. The idea is that, if `F : Filter Î±`
and `V : Set Î²`, the statement
`V âˆˆ Filter.map f F â†” f â»Â¹' V âˆˆ F` should be true
by definition.-/

#print Filter.map

-- This is compatible to the definition for sets.
example {s : Set Î±} (f : Î± â†’ Î²) :
    Filter.map f (Filter.principal s) = Filter.principal (f '' s) := by
  ext A
  change f â»Â¹'A âˆˆ ğ“Ÿ s â†” A âˆˆ ğ“Ÿ (f '' s) -- ğ“Ÿ = \ + MCP
  rw [mem_principal, mem_principal]
  exact Set.image_subset_iff.symm

/- We can now reformulate the notation of convergence
using these notions. The idea is that, for example,
if `f : â„ â†’ â„`, then `f` tends to `b : â„` at `a : â„`
if, for every `x : â„` close enough to `a`, its image
`f(x)` is close enough to `b`. In other words, `f` sends
the "set of elements close enough to `a`" to a "subset"
of "the set of elements close enough to `b`".
-/

def Tendstoâ‚‚ {X Y : Type*} (f : X â†’ Y) (F : Filter X)
    (G : Filter Y) := Filter.map f F â‰¤ G
-- This is the mathlib definition.
-- Tendstoâ‚ : âˆ€ U âˆˆ G, f â»Â¹' U âˆˆ F i.e.

example {X Y : Type*} (f : X â†’ Y) (F : Filter X) (G : Filter Y) :
    Tendstoâ‚‚ f F G â†” Tendstoâ‚ f F G :=
  Iff.rfl

/- Now to prove the compatibility of limits with compositions,
we use the properties of `Filter.map`.-/
#print Filter.map_mono -- `Filter.map f` is monotone.
-- If F â‰¤ F', then map f F â‰¤ map f F'.
#print Filter.map_map -- `Filter.map (g âˆ˜ f) = Filter.map g âˆ˜ Filter.map f`

-- Compatibility with composition.
example {X Y Z : Type*} (f : X â†’ Y) (g : Y â†’ Z) (F : Filter X)
    (G : Filter Y) (H : Filter Z) :
    Tendstoâ‚‚ f F G â†’ Tendstoâ‚‚ g G H â†’ Tendstoâ‚‚ (g âˆ˜ f) F H := by
  intro h h'
  change map (g âˆ˜ f) F â‰¤ H
  rw [â† map_map]
  refine le_trans ?_ h'
  apply map_mono
  exact h

/- Among the other "set" operations, we have preimages, which
are called `Filter.comap` for filters.-/
#print Filter.comap --why this definition?
-- f : Î± â†’ Î² and G a filter on Î²
-- s âˆˆ comap f G â†” âˆƒ t âˆˆ G, f â»Â¹' t âŠ† s

example (s : Set Î²) (f : Î± â†’ Î²) : comap f (ğ“Ÿ s) = ğ“Ÿ (f â»Â¹' s) := by
  simp only [comap_principal]

/- If `f : Î± â†’ Î²` is a function and `s : Set Î±`, `t : Set Î²`, then
we have `f '' s âŠ† t` if and only if `s âŠ† f â»Â¹' t`. We want to
have the same property for filters, i.e. for `F : Filter Î±` and
`G : Filter Î²`, we want `Filter.map f F â‰¤ G â†” F â‰¤ Filter.comap f G`.
(In technical terms, this means that `Filter.map f` and `Filter.comap f`
form a Galois connection, i.e. an adjunction between poset categories.)
-/
#check Filter.map_le_iff_le_comap

example {f : Î± â†’ Î²} {F : Filter Î±} {G : Filter Î²} :
    Filter.map f F â‰¤ G â†” F â‰¤ Filter.comap f G := by
  constructor
  Â· intro h A hA
    rw [Filter.mem_comap] at hA
    obtain âŸ¨B, hbâŸ© := hA
    have := h hb.1
    rw [Filter.mem_map] at this
    exact Filter.mem_of_superset this hb.2
  Â· intro h B hB
    rw [Filter.mem_map]
    apply h
    rw [Filter.mem_comap]
    use B

#print Tendstoâ‚‚

/- Using `Filter.comap`, we can give an equivalent definition
of `Tendsto`.-/

def Tendstoâ‚ƒ {X Y : Type*} (f : X â†’ Y) (F : Filter X)
    (G : Filter Y) := F â‰¤ Filter.comap f G
-- But mathlib uses the definition with `Filter.map`.

example {X Y : Type*} (f : X â†’ Y) (F : Filter X) (G : Filter Y) :
    Tendstoâ‚‚ f F G â†” Tendstoâ‚ƒ f F G := by
  rw [Tendstoâ‚‚, Tendstoâ‚ƒ, map_le_iff_le_comap]

/- `Filter.comap` is also compatible with composition of
functions, but just like for preimages, this reverses the
order:-/
#print Filter.comap_comap
-- Filter.comap m (Filter.comap n F) = Filter.comap (n âˆ˜ m) F

/- We can use `Filter.comap` to define the intersection of a
filter with a set of `Î±`. For example, our `nhds_left a` at the
start is the intersection of `nhds a` and of `Set.Iic a`
(well, almost... this intersection would be a filter on
`Set.Iic a`).-/
example (a : â„) : nhds_left a = Filter.map Subtype.val
    (Filter.comap (Subtype.val : Set.Iic a â†’ â„) (nhds a)) := by
  ext A
  simp only [nhds_left, gt_iff_lt, Filter.mem_mk, mem_setOf_eq, mem_map, mem_comap]
  constructor
  Â· intro âŸ¨Îµ, hpos, hâŸ©
    use Ioo (a - Îµ) (a + Îµ)
    constructor
    Â· rw [(nhds_basis_Ioo_pos a).mem_iff]
      use Îµ, hpos
    Â· intro âŸ¨x, hxâŸ©
      simp only [mem_preimage, mem_Ioo, and_imp]
      intro hleft _
      exact h âŸ¨hleft, hxâŸ©
  Â· intro âŸ¨U, hUâ‚, hUâ‚‚âŸ©
    rw [(nhds_basis_Ioo_pos a).mem_iff] at hUâ‚
    obtain âŸ¨Îµ, hpos, hâŸ© := hUâ‚
    use Îµ, hpos
    intro x âŸ¨hxâ‚, hxâ‚‚âŸ©
    have : âŸ¨x, hxâ‚‚âŸ© âˆˆ (Subtype.val : Iic a â†’ â„) â»Â¹' U := by
      simp only [mem_preimage]
      exact h âŸ¨hxâ‚, lt_of_le_of_lt hxâ‚‚ ((lt_add_iff_pos_right _).mpr hpos)âŸ©
    exact mem_preimage.mp (hUâ‚‚ this)

example (s : Set Î±) (F : Filter Î±) : Filter s :=
  comap Subtype.val F
  -- Subtype.val : s â†’ Î±
-- If t : Set s, t âˆˆ comap Subtype.val F â†” âˆƒ u âˆˆ F, s âˆ© u âŠ† t

/-
Other operations that we can expect to have on filters
if they are "generalized sets" are `sup` and `inf`, even
for infinite families.

(Remark: filters actually correspond to closed sets in some
compact topological space. The `inf` operations is just the
intersection, and the `sup` of a family of closed sets is the
closure of its union. But this shows that we cannot expect an
operation corresponding to the complement on sets.)
-/

-- The `sup` and `inf` for two filters.
#print Filter.mem_sup -- s âˆˆ F âŠ” G â†” s âˆˆ F âˆ§ s âˆˆ G
#print Filter.mem_inf_iff -- s âˆˆ F âŠ“ G â†” âˆƒ tâ‚ âˆˆ F, âˆƒ tâ‚‚ âˆˆ G, s = tâ‚ âˆ© tâ‚‚

-- The case of an arbitrary family:
#check Filter.mem_iSup -- indexed Sup
-- âˆ€ {Î± : Type u} {Î¹ : Sort x} {x : Set Î±} {f : Î¹ â†’ Filter Î±},
--  x âˆˆ â¨† f â†” âˆ€ (i : Î¹), x âˆˆ f i -- \ + bigsup
#check Filter.mem_iInf
--âˆ€ {Î± : Type u} {Î¹ : Type u_2} {f : Î¹ â†’ Filter Î±} {U : Set Î±},
--  U âˆˆ â¨… i, f i â†” âˆƒ I : Set Î¹, I.Finite âˆ§
-- âˆƒ V : â†‘I â†’ Set Î±, (âˆ€ (i : â†‘I), V i âˆˆ f â†‘i) âˆ§ U = â‹‚ i, V i -- \ + biginf

-- What happens if we allow infinite intersections?

-- F â‰¤ ğ“Ÿ s â†” s âˆˆ F

example (F : Filter Î±) :
    F = â¨… (s : F.sets), ğ“Ÿ s := by  -- F is the inf of the family of
    -- principal filters greater than or equal to F
  apply le_antisymm
  Â· simp only [le_iInf_iff, le_principal_iff, Subtype.forall, Filter.mem_sets, imp_self,
    implies_true]
  Â· intro s hs
    rw [mem_iInf]
    use {âŸ¨s, hsâŸ©} -- ok because âŸ¨s, hsâŸ© : F.sets
    constructor
    Â· simp only [finite_singleton]
    Â· use fun p â†¦ p.1
      constructor
      Â· simp only [mem_principal, subset_refl, implies_true]
      Â· simp only [iInter_coe_set, mem_singleton_iff, iInter_iInter_eq_left]


-- A finite intersection of members of a filter is in the
-- the filter. These are both `simp` lemmas.
#print Filter.sInter_mem
#print Filter.iInter_mem


/- We also have a smallest filter `âŠ¥` (the principal filter
generated by the empty set, also called the trivial filter)
and a biggest filter `âŠ¤` (generated by the universal set).-/

example (s : Set Î±) : s âˆˆ (âŠ¥ : Filter Î±) := Filter.mem_bot

example (s : Set Î±) : s âˆˆ (âŠ¤ : Filter Î±) â†” s = univ := by
  simp only [mem_top]

/- Some lemmas require filters to be nontrivial, so there is
a class `Filter.NeBot`, and these lemma take `F.NeBot` as an
instance parameter.-/

#print Filter.NeBot

-- Mathlib knows that a lot of filters are not trivial.
#synth Filter.NeBot (Filter.atTop (Î± := â„•))
#synth Filter.NeBot (nhds (1 : â„))

#print Filter.map_neBot
-- This is an instance, so mathlib automatically knows that,
-- if `F` is not trivial, then so is its `map` by any function.

/- If `F : Filter Î±` and `G : Filter Î²`, we can define the
product of `F` and `G`, which is a filter on `Î± Ã— Î²`. We
write this `Filter.prod F G` or `F Ã—Ë¢ G`.
For example, if `a,b : â„`, then `nhds a Ã—Ë¢ nhds b` will
be `nhds âŸ¨a, bâŸ©`, the filter of neighborhoods of `âŸ¨a, bâŸ©`
in `â„ Ã— â„`.-/

#print Filter.prod
-- F Ã—Ë¢ G = (Filter.comap Prod.fst F) âŠ“ (Filter.comap Prod.snd G)
-- This means that `F Ã—Ë¢ G` is the biggest filter `H` on
-- `Î± Ã— Î²` such that `Filter.map Prod.fst H â‰¤ F` and
-- `Filter.map Prod.snd H â‰¤ G`.

#check Filter.mem_prod_iff
-- s âˆˆ f Ã—Ë¢ g â†” âˆƒ tâ‚ âˆˆ f, âˆƒ tâ‚‚ âˆˆ g, tâ‚ Ã—Ë¢ tâ‚‚ âŠ† s

example (s : Set Î±) (t : Set Î²) : Set (Î± Ã— Î²) := s Ã—Ë¢ t -- \ + x + \ + ^s
example (F : Filter Î±) (G : Filter Î²) : Filter (Î± Ã— Î²) := F Ã—Ë¢ G

/- Actually, we also have arbitrary products of filters.-/
#print Filter.pi -- same formula as for `Filter.prod`:
-- â¨… i, Filter.comap (Function.eval i) (f i)

/- Filter bases:
This is a generalization of principal filters. If `F : Filter Î±`
and `s : Î¹ â†’ Set Î±` is a family of sets of `Î±`, then we say
that `s` is a basis of `F` if
`âˆ€ (t : Set Î±), t âˆˆ F â†” âˆƒ (i : Î¹), s i âŠ† t`.

This is expressed by a `Prop`-valued structure `Filter.HasBasis`.
-/

#print Filter.HasBasis
/-
/-- We say that a filter `l` has a basis `s : Î¹ â†’ Set Î±` bounded by `p : Î¹ â†’ Prop`,
if `t âˆˆ l` if and only if `t` includes `s i` for some `i` such that `p i`. -/
structure HasBasis (l : Filter Î±) (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set Î±) : Prop where
  /-- A set `t` belongs to a filter `l` iff it includes an element of the basis. -/
  mem_iff' : âˆ€ t : Set Î±, t âˆˆ l â†” âˆƒ i, p i âˆ§ s i âŠ† t

-/

-- Note the arguments: we don't just give `s : Î¹ â†’ Set Î±`, but
-- also a `p : Î¹ â†’ Prop` and only use as basis  the `s i`
-- for the `i : Î¹` such that `p i` holds. This is convenient
-- because `s` can then be defined on a more natural type, as in
-- the following example.
example (xâ‚€ : â„) : HasBasis (ğ“ xâ‚€) (p := fun Îµ : â„ â†¦ 0 < Îµ)
    (s := fun Îµ â†¦ Ioo (xâ‚€ - Îµ) (xâ‚€ + Îµ)) :=
  nhds_basis_Ioo_pos xâ‚€

example : HasBasis (atTop : Filter â„•) (fun _ : â„• â†¦ True) (fun n â†¦ Ici n) :=
  atTop_basis

-- Now to check convergence along filters, it suffices to
-- use the sets in the basis.
#check Filter.HasBasis.tendsto_iff  -- very useful result!

example (u : â„• â†’ â„) (xâ‚€ : â„) :
    Tendsto u atTop (ğ“ xâ‚€) â†”
    âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, u n âˆˆ Ioo (xâ‚€ - Îµ) (xâ‚€ + Îµ) := by
  rw [Filter.HasBasis.tendsto_iff atTop_basis (nhds_basis_Ioo_pos xâ‚€)]
  simp

example (f : â„ â†’ â„) (a b : â„) :
    Tendsto f (ğ“ a) (ğ“ b) â†”
    âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x, x âˆˆ Ioo (a - Î´) (a + Î´) â†’
    f x âˆˆ Ioo (b - Îµ) (b + Îµ) := by
  rw [Filter.HasBasis.tendsto_iff (nhds_basis_Ioo_pos a) (nhds_basis_Ioo_pos b)]

#check nhds_basis_Ioo -- basis is (b, c) with b < a < c
#check nhds_basis_Ioo_pos  -- basis is (a - Îµ, a + Îµ) with Îµ > 0

example (f : â„ â†’ â„) (a b : â„) :
    Tendsto f (ğ“ a) (ğ“ b) â†” âˆ€ (U : Set â„), b âˆˆ U âˆ§ IsOpen U â†’
    âˆƒ (V : Set â„), (a âˆˆ V âˆ§ IsOpen V) âˆ§ (âˆ€ x âˆˆ V, f x âˆˆ U) := by
  rw [HasBasis.tendsto_iff (nhds_basis_opens a) (nhds_basis_opens b)]
-- I had switched `U` and `V` in `âˆ€ x âˆˆ V, f x âˆˆ U`

#check nhds_basis_opens

-- If we know a basisof a filter, it is easy to describe
-- its members.
#check Filter.HasBasis.mem_iff

example (A : Set â„•) : A âˆˆ atTop â†” âˆƒ n, Set.Ici n âŠ† A := by
  rw [Filter.HasBasis.mem_iff (atTop_basis)]
  simp

/- Another use of filters is that they give a convenient
way to talk about properties that are true for `x` big enough,
for `x` close enough to a fixed point, for almost all `x` etc.

For this, we use the function `Filter.Eventually`. If
`p : Î± â†’ Prop` and `F : Filter Î±`, then `Filter.Eventually p F`
(or `F.Eventually p`)
means that `{x | p x}` is an element of `F`. Intuitively, this
means that `p` is true on the "set" corresponding to `F`.

The notation for this is:
`âˆ€á¶  x in F, p x`. (\ + forall + \ + ^f)
-/

example : âˆ€á¶  n in atTop (Î± := â„•), 2 â‰¤ n := by
  dsimp [Filter.Eventually]
  simp
  use 2
  simp

example : âˆ€á¶  x in nhds (0 : â„), |x| â‰¤ 1/2 := by
  dsimp [Filter.Eventually]
  rw [(nhds_basis_Ioo_pos 0).mem_iff]
  use 1/2
  constructor
  Â· simp only [one_div, inv_pos, Nat.ofNat_pos]
  Â· simp only [zero_sub, zero_add]
    intro x âŸ¨hxâ‚, hxâ‚‚âŸ©
    rw [mem_setOf_eq, abs_le]
    exact âŸ¨le_of_lt hxâ‚, le_of_lt hxâ‚‚âŸ©

/- Now let's see what the properties of a filter say about `âˆ€á¶ `:

(1) `âŠ¤ âˆˆ F` means that: `âˆ€ x, p x â†’ âˆ€á¶  x in F, p x`.-/
#check Eventually.of_forall

/-(2) The stability of `F` by taking a superset means that, if
`q : Î± â†’ Prop` is another function, and if
`âˆ€á¶  x, p x` and `âˆ€ x, p x â†’ q x`, then `âˆ€á¶  x, q x`.-/
#check Eventually.mono

/-(3) The stability of `F` by intersections means that, if
`âˆ€á¶  x in F, p x` and `âˆ€á¶  x in F, q x`, then
`âˆ€á¶  x in F, p x âˆ§ q x`.-/
#check Filter.Eventually.and

example (P Q : â„• â†’ Prop) (hP : âˆ€á¶  n in atTop, P n) (hQ : âˆ€á¶  n in atTop, Q n) :
    âˆ€á¶  n in atTop, P n âˆ§ Q n := by
  dsimp [Filter.Eventually] at hP hQ âŠ¢
  exact atTop.inter_sets hP hQ

/- There are two special cases of `Filter.Eventually` for equalities
and inequalities:-/
#print Filter.EventuallyEq
#print Filter.EventuallyLE


/- They have special notation too:-/
example (u v : â„• â†’ â„) : (âˆ€á¶  n in atTop, u n = v n) â†” u =á¶ [atTop] v := Iff.refl _

example (u v : â„• â†’ â„) : (âˆ€á¶  n in atTop, u n â‰¤ v n) â†” u â‰¤á¶ [atTop] v := Iff.refl _

-- For example, two sequences that are eventually equal
-- for the filter `atTop` have the same limit.
example (u v : â„• â†’ â„) (h : u =á¶ [atTop] v) (xâ‚€ : â„) :
    Tendsto u atTop (ğ“ xâ‚€) â†” Tendsto v atTop (ğ“ xâ‚€) :=
  tendsto_congr' h

/- There is a tactic called `filter_upwards` to deal with goals
of the `âˆ€á¶  s in F, ...`.-/

/-- From the documentation:
`filter_upwards [hâ‚, â‹¯, hâ‚™]` replaces a goal of the form `s âˆˆ f` and terms
`hâ‚ : tâ‚ âˆˆ f, â‹¯, hâ‚™ : tâ‚™ âˆˆ f` with `âˆ€ x, x âˆˆ tâ‚ â†’ â‹¯ â†’ x âˆˆ tâ‚™ â†’ x âˆˆ s`.
The list is an optional parameter, `[]` being its default value.

`filter_upwards [hâ‚, â‹¯, hâ‚™] with aâ‚ aâ‚‚ â‹¯ aâ‚–` is a short form for
`{ filter_upwards [hâ‚, â‹¯, hâ‚™], intros aâ‚ aâ‚‚ â‹¯ aâ‚– }`.

`filter_upwards [hâ‚, â‹¯, hâ‚™] using e` is a short form for
`{ filter_upwards [h1, â‹¯, hn], exact e }`.

Combining both shortcuts is done by writing
`filter_upwards [hâ‚, â‹¯, hâ‚™] with aâ‚ aâ‚‚ â‹¯ aâ‚– using e`.
Note that in this case, the `aáµ¢` terms can be used in `e`.
-/

-- Without `filter_upwards`.
example (P Q R : â„• â†’ Prop) (hP : âˆ€á¶  n in atTop, P n) (hQ : âˆ€á¶  n in atTop, Q n)
    (hR : âˆ€á¶  n in atTop, P n âˆ§ Q n â†’ R n) : âˆ€á¶  n in atTop, R n := by
  have := hP.and (hQ.and hR)
  apply Eventually.mono this
  rintro n âŸ¨h, h', h''âŸ©
  exact h'' âŸ¨h, h'âŸ©

example (P Q R : â„• â†’ Prop) (hP : âˆ€á¶  n in atTop, P n) (hQ : âˆ€á¶  n in atTop, Q n)
    (hR : âˆ€á¶  n in atTop, P n âˆ§ Q n â†’ R n) : âˆ€á¶  n in atTop, R n := by
  filter_upwards [hP, hQ, hR] with n h h' h'' using h'' âŸ¨h, h'âŸ©

-- An exercise: if the sequence `u` converges to `x` and
-- `u n` is in `M` for `n` big enough, then `x` is in the closure
-- of `M`.

example (u : â„• â†’ â„) (M : Set â„) (x : â„) (hux : Tendsto u atTop (ğ“ x))
    (huM : âˆ€á¶  n in atTop, u n âˆˆ M) : x âˆˆ closure M := by
  rw [mem_closure_iff_clusterPt]
  change (ğ“ x âŠ“ ğ“Ÿ M).NeBot
  apply neBot_of_le (f := map u atTop)
  rw [le_inf_iff]
  refine âŸ¨hux, ?_âŸ©
  refine le_trans (map_mono (m := u) (le_principal_iff.mpr huM)) ?_
  simp only [map_principal, le_principal_iff, mem_principal, image_subset_iff]
  intro x
  simp

--Useful lemmas for the exercise:
#check mem_closure_iff_clusterPt
#print ClusterPt -- note that `ClusterPt x F` means by definition
                 -- that `ğ“ x âŠ“ F` is not the `âŠ¥` filter
#check le_principal_iff
#check neBot_of_le

/- Another filter notion is `Filter.Frequently`. You
would use it for example to express something like
"there exist arbitrarily large `n` in `â„•` such that so and so".-/

#print Filter.Frequently
-- `Filter.Frequently p F` means `Â¬âˆ€á¶  (x : Î±) in f, Â¬p x` i.e.
-- `{x | Â¬p x} âˆ‰ F`. It is written `âˆƒá¶  x in F, p x`.
-- This is actually equivalent to saying that, for every `A âˆˆ F`,
-- there exists `x âˆˆ A` such that `p x`. Don't believe me?

example (p : Î± â†’ Prop) (F : Filter Î±) :
    (âˆƒá¶  x in F, p x) â†” âˆ€ A âˆˆ F, âˆƒ x âˆˆ A, p x := by
  constructor
  Â· intro h A hA
    by_contra habs
    push_neg at habs
    have hsub : A âŠ† {x | Â¬p x} := by
      intro x hx
      simp only [mem_setOf_eq, habs x hx, not_false_eq_true]
    have := F.mem_of_superset hA hsub
    exact h this
  Â· dsimp [Filter.Frequently]
    intro h habs
    obtain âŸ¨x, hxâ‚, hxâ‚‚âŸ© := h _ habs
    simp only [mem_setOf_eq] at hxâ‚
    exact hxâ‚ hxâ‚‚

-- Of course, this result is already in mathlib:
#check Filter.frequently_iff

example : âˆƒá¶  n in atTop (Î± := â„•), Nat.Prime n := by
  rw [frequently_atTop]
  intro a
  obtain âŸ¨p, hpâ‚, hpâ‚‚âŸ© := Nat.exists_infinite_primes a
  use p
