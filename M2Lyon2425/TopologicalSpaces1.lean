import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.MeasureTheory.OuterMeasure.AE
import Mathlib.MeasureTheory.Measure.Lebesgue.Basic
import Mathlib.MeasureTheory.Measure.MeasureSpaceDef

open Filter Set Topology

/- Filters. -/

-- A filter `F` on a type `Î±` is set in `Set Î±` (i.e. a set of
-- sets in `Î±`) such that:
-- (1) The biggest set `âŠ¤` is in `F`;
-- (2) If `x,y : Set Î±` and `x âŠ† y`, then `x âˆˆ F` implies that `y âˆˆ F`;
-- (3) `F` is stable by finite intersections.

-- More precisely, `Filter` is a structure:
#print Filter

variable {Î± Î² : Type*}

/-- If `F` is a filter on `Î±`, and `U` a subset of `Î±` then we can
write `U âˆˆ F` as on paper, thanks to the following declaration: -/
instance instMembership : Membership (Set Î±) (Filter Î±) :=
  âŸ¨fun U F => U âˆˆ F.setsâŸ©
-- NB: comment this, this is already declare in mathlib.

-- Examples:

-- If `a : Î±`, the set of sets containing `a` is a filter,
-- (and even an ultrafilter).
example (a : Î±) : Filter Î± where
  sets := {A | a âˆˆ A}
  univ_sets := sorry
  sets_of_superset := sorry
  inter_sets := sorry

-- More generally, if `s : Set Î±`, the set of sets containing `s`
-- is a filter.
example (s : Set Î±) : Filter Î± where
  sets := {A : Set Î± | s âŠ† A}
  univ_sets := sorry
  sets_of_superset := sorry
  inter_sets := sorry

-- This is called a principal filter, `Filter.principal` in mathlib:
#print Filter.principal

-- The set of sets of natural integers (or real numbers, or
-- rational numbers...) that are "big enough" is a filter.
example : Filter â„• where
  sets := {A | âˆƒ n, Set.Ici n âŠ† A}
  univ_sets := sorry
  sets_of_superset := sorry
  inter_sets := sorry

-- This filter is called `Filter.atTop`:
#print Filter.atTop
#print Filter.mem_atTop

-- There is also a filter for "small enough" elements, called
-- `Filter.atBot`.


-- The neighborhoods of a point in `â„` (or any metric or more
-- generally topological space):
example (a : â„) : Filter â„ where
  sets := {A | âˆƒ Îµ > 0, Set.Ioo (a - Îµ) (a + Îµ) âŠ† A}
  univ_sets := sorry
  sets_of_superset := sorry
  inter_sets := sorry

example (a : â„) : Filter â„ where
  sets := {A | âˆƒ (U : Set â„), IsOpen U âˆ§ a âˆˆ U âˆ§ U âŠ† A}
  univ_sets := sorry
  sets_of_superset := sorry
  inter_sets := sorry

-- This filter is called `nhs` or `ğ“` (\ + nhds):
#print nhds

-- If `a : â„`, we can also look at the set of subsets of `â„`
-- that contain an interval `(a-Îµ,a]` with `Îµ > 0`, and this is
-- still a filter.
def nhds_left (a : â„) : Filter â„ where
  sets := {A | âˆƒ Îµ > 0, Set.Ioc (a - Îµ) a âŠ† A}
  univ_sets := sorry
  sets_of_superset := sorry
  inter_sets := sorry

/- If `Î±` has a measure `Î¼`, then we have a filter
`MesureTheory.ae Î¼` whose elements are co-null sets (i.e.
measurable sets whose complement has measure zero). This
is "the set of almost all elements of `Î±`".
-/
#print MeasureTheory.ae

open MeasureTheory in
#check ae (volume : Measure â„)

/- Why filters?-/

/- Filters are (among other things) a very convenient way
to talk about convergence.

For example, consider a function `f : â„ â†’ â„` and `a,b : â„`.

Suppose that we want to say that the limit of `f` at `a`
is `b`. This means that, for every `Îµ > 0`, there exists
`Î´ > 0` such that `f` sends `(a - Î´, a + Î´)` to
`(b - Îµ, b + Îµ)`.
We can reformulate this by saying that `f â»Â¹' (b - Îµ, b + Îµ)`
is in the filter of neighborhoods of `a` for every `Îµ`, which
means: `âˆ€ (A : nhds b), f â»Â¹' A âˆˆ nhds a`.

But now suppose to say that `f(x)` tends to `b` as `x` tends to
on the left. This means that for every `Îµ > 0`, there exists
`Î´ > 0` such that `f` sends `(a - Î´, a]` to `(b - Îµ, b + Îµ)`.
With filters, this becomes: `âˆ€ (A : nhds b), f â»Â¹'A âˆˆ nhds_left a`.

We can similarly express things like "`f(x)` approaches `b`
on the right when `x` approaches `a` on the left", etc.

Now suppose that we want to say `f(x)` tends to `b` as `x`
tends to `+ âˆ`. This means that, for every `Îµ > 0`, there
exists `M : â„` such that `f` sends `[M, + âˆ)` to
`(b - Îµ, b + Îµ)`. Or, with filters:
`âˆ€ (A : nhds b), f â»Â¹' A âˆˆ Filter.atTop`.
We could similarly express the fact that `f(x)` approaches
`b` from the left as `x` tends to `+ âˆ`, by using `nhds_left b`
instead of `nhds`.

Similarly, if `u : â„• â†’ â„` is a sequence (here with real values,
but it could have values in any topological space), we can
express the fact that `u` converges to `b : â„` with filters:
`âˆ€ (A : nhds b), f â»Â¹' b âˆˆ Filter.atTop`.

Note that all these definitions of convergence can be written
in exactly the same way once we decide to use filters. This is
already nice, but it starts being really powerful when we
want to prove theorems about limits.

For example, let `f,g : â„ â†’ â„` and let `a,b,c : â„`. We can
prove that, if `f(x)` tends to `b` as `x` tends to `a`
and `g(y)` tends to `c` as `y` tends to `b`, then `(g âˆ˜ f) (x)`
tends to `c` as `x` tends to `a`.
But then suppose that we want to use that, if `f(x)` tends to
`b` on the right as `x` tends to `a` on the left and `g(y)`
tends to `c` on the left as `y` tends to `b` on the right, then
`(g âˆ˜ f) (x)` tends to `c` on the left as `x` tends to `a` on
the left. On paper, we can just say that "the proof is similar",
but Lean won't accept that, so we would have to rewrite the
proof. Now think about all the different possibilities
(including limits at infinity, limits as `x` is only in a certain
subset etc), and ask yourselves if you really want to write the
resulting 3487 lemmas (conservative estimation).

If instead we can express everything with filters, then
we only need to prove each statement once.
-/

-- First attempt to define convergence: `f : X â†’ Y` is a
-- function, we have a filter `F` on `X`, a filter `G` on
-- `Y`, and we want to say `f` tends to `Y` along `X`.
-- We generalize the definition that appeared before.

def Tendstoâ‚ {X Y : Type*} (f : X â†’ Y) (F : Filter X)
    (G : Filter Y) := âˆ€ V âˆˆ G, f â»Â¹' V âˆˆ F

-- Compatibility with composition.
example {X Y Z : Type*} (f : X â†’ Y) (g : Y â†’ Z) (F : Filter X)
    (G : Filter Y) (H : Filter Z) :
    Tendstoâ‚ f F G â†’ Tendstoâ‚ g G H â†’ Tendstoâ‚ (g âˆ˜ f) F H := by sorry

/- An intuitive way to think about filters, and a reformulation
of convergence.

Remember that, for every `s : Set Î±`, we have the principal filter
generated by `s`: it is the filter whose elements are sets
containing `s`.

We think of this filter as standing in for `s`, and we think of
more general filters as "generalized sets" of `Î±`. With this
point of view, if `F` is a filter on `Î±` and `s : Set Î±`, saying
that `s âˆˆ F` means that `s` "contains" the corresponding
"generalized set".

For example, if `Î±` is `â„` (or more generally if `Î±` has a
topology) and `a : Î±`, then `ğ“ a` is "the set of elements
close enough to `a`".
If `Î±` has a (pre)order, then `Filter.atTop` is "the set
of elements that are big enough".
If `Î±` has a measure `Î¼`, then we have a filter
`MesureTheory.ae Î¼` whose elements are co-null sets (i.e.
measurable sets whose complement has measure zero). This
is "the set of almost all elements of `Î±`".

(If you know what this means, filters on `X` actually
correspond to closed sets of the Stone-Cech compactification
of the discrete space `X`. If you don't know what this means,
don't worry.)
-/

/- Now that we think of filters as generalized sets,
let's extend some set notions to them.

The first is the order relation: sets on `Î±` are
ordered by inclusion. How does it translate to filters?
Well, it means that every set that contains `t` also
contains `s`:
-/

example (s t : Set Î±) : s âŠ† t â†”
    (Filter.principal t).sets âŠ† (Filter.principal s).sets := sorry

-- So this is how we define order on filters:
#print Filter.le_def  -- F â‰¤ G â†” âˆ€ x âˆˆ G, x âˆˆ F

example (F : Filter Î±) (s : Set Î±) :
    Filter.principal s â‰¤ F â†” âˆ€ A âˆˆ F, s âŠ† A := by sorry

example (F : Filter Î±) (s : Set Î±) :
    F â‰¤ Filter.principal s â†” s âˆˆ F := sorry

/- The second notion is the image of a filter by
a function `f : Î± â†’ Î²`. This operation is called
`Filter.map`. The idea is that, if `F : Filter Î±`
and `V : Set Î²`, the statement
`V âˆˆ Filter.map f F â†” f â»Â¹' V âˆˆ F` should be true
by definition.-/

#print Filter.map

-- This is compatible to the definition for sets.
example {s : Set Î±} (f : Î± â†’ Î²) :
    Filter.map f (Filter.principal s) = Filter.principal (f '' s) := sorry

/- We can now reformulate the notation of convergence
using these notions. The idea is that, for example,
if `f : â„ â†’ â„`, then `f` tends to `b : â„` at `a : â„`
if, for every `x : â„` close enough to `a`, its image
`f(x)` is close enough to `b`. In other words, `f` sends
the "set of elements close enough to `a`" to a "subset"
of "the set of elements close enough to `b`".
-/

def Tendstoâ‚‚ {X Y : Type*} (f : X â†’ Y) (F : Filter X)
    (G : Filter Y) := Filter.map f F â‰¤ G
-- This is the mathlib definition.

example {X Y : Type*} (f : X â†’ Y) (F : Filter X) (G : Filter Y) :
    Tendstoâ‚‚ f F G â†” Tendstoâ‚ f F G :=
  Iff.rfl

/- Now to prove the compatibility of limits with compositions,
we use the properties of `Filter.map`.-/
#print Filter.map_mono -- `Filter.map f` is monotone.
#print Filter.map_map -- `Filter.map (g âˆ˜ f) = Filter.map g âˆ˜ Filter.map f`

-- Compatibility with composition.
example {X Y Z : Type*} (f : X â†’ Y) (g : Y â†’ Z) (F : Filter X)
    (G : Filter Y) (H : Filter Z) :
    Tendstoâ‚‚ f F G â†’ Tendstoâ‚‚ g G H â†’ Tendstoâ‚‚ (g âˆ˜ f) F H := by sorry

/- Among the other "set" operations, we have preimages, which
are called `Filter.comap` for filters.-/
#print Filter.comap --why this definition?

/- If `f : Î± â†’ Î²` is a function and `s : Set Î±`, `t : Set Î²`, then
we have `f '' s âŠ† t` if and only if `s âŠ† f â»Â¹' t`. We want to
have the same property for filters, i.e. for `F : Filter Î±` and
`G : Filter Î²`, we want `Filter.map f F â‰¤ G â†” F â‰¤ Filter.comap f G`.
(In technical terms, this means that `Filter.map f` and `Filter.comap f`
form a Galois connection, i.e. an adjunction between poset categories.)
-/
#print Filter.map_le_iff_le_comap

example {f : Î± â†’ Î²} {F : Filter Î±} {G : Filter Î²} :
    Filter.map f F â‰¤ G â†” F â‰¤ Filter.comap f G := by sorry

/- Using `Filter.comap`, we can give an equivalent definition
of `Tendsto`.-/

def Tendstoâ‚ƒ {X Y : Type*} (f : X â†’ Y) (F : Filter X)
    (G : Filter Y) := F â‰¤ Filter.comap f G
-- But mathlib uses the definition with `Filter.map`.

example {X Y : Type*} (f : X â†’ Y) (F : Filter X) (G : Filter Y) :
    Tendstoâ‚‚ f F G â†” Tendstoâ‚ƒ f F G := sorry

/- `Filter.comap` is also compatible with composition of
functions, but just like for preimages, this reverses the
order:-/
#print Filter.comap_comap
-- Filter.comap m (Filter.comap n F) = Filter.comap (n âˆ˜ m) F

/- We can use `Filter.comap` to define the intersection of a
filter with a set of `Î±`. For example, our `nhds_left a` at the
start is the intersection of `nhds a` and of `Set.Iic a`
(well, almost... this intersection would be a filter on
`Set.Iic a`).-/
example (a : â„) : nhds_left a = Filter.map Subtype.val
    (Filter.comap (Subtype.val : Set.Iic a â†’ â„) (nhds a)) := sorry

/-
Other operations that we can expect to have on filters
if they are "generalized sets" are `sup` and `inf`, even
for infinite families.

(Remark: filters actually correspond to closed sets in some
compact topological space. The `inf` operations is just the
intersection, and the `sup` of a family of closed sets is the
closure of its union. But this shows that we cannot expect an
operation corresponding to the complement on sets.)
-/

-- The `sup` and `inf` for two filters.
#print Filter.mem_sup -- s âˆˆ F âŠ” G â†” s âˆˆ F âˆ§ s âˆˆ G
#print Filter.mem_inf_iff -- s âˆˆ F âŠ“ G â†” âˆƒ tâ‚ âˆˆ F, âˆƒ tâ‚‚ âˆˆ G, s = tâ‚ âˆ© tâ‚‚

-- The case of an arbitrary family:
#print Filter.mem_iSup
-- âˆ€ {Î± : Type u} {Î¹ : Sort x} {x : Set Î±} {f : Î¹ â†’ Filter Î±},
--  x âˆˆ iSup f â†” âˆ€ (i : Î¹), x âˆˆ f i
#print Filter.mem_iInf
--âˆ€ {Î± : Type u} {Î¹ : Type u_2} {s : Î¹ â†’ Filter Î±} {U : Set Î±},
--  U âˆˆ â¨… i, s i â†” âˆƒ I, I.Finite âˆ§
-- âˆƒ V, (âˆ€ (i : â†‘I), V i âˆˆ s â†‘i) âˆ§ U = â‹‚ i, V i

-- What happens if we allow infinite intersections?

example (F : Filter Î±) :
    F = â¨… (s : F.sets), Filter.principal s := by
  ext s
  rw [Filter.mem_iInf]
  constructor
  Â· intro hs
    use {âŸ¨s, hsâŸ©}
    constructor
    Â· simp only [Set.finite_singleton]
    Â· use fun s â†¦ s.1
      simp
/-      constructor
      Â· intro âŸ¨t, htâŸ©
        simp only [Set.mem_singleton_iff] at ht
        simp_rw [ht]
        simp only [Filter.mem_principal, subset_refl]
      Â· simp only [Set.iInter_coe_set, Set.mem_singleton_iff,
          Set.iInter_iInter_eq_left]-/
  Â· intro âŸ¨I, hI, âŸ¨f, hfâ‚, hfâ‚‚âŸ©âŸ©
    rw [hfâ‚‚]
    have : Finite â†‘I := Set.finite_coe_iff.mpr hI
    rw [Filter.iInter_mem]
    intro i
    suffices h : F â‰¤ Filter.principal â†‘â†‘i by
      apply h
      exact hfâ‚ i
    simp; exact i.1.2

-- A finite intersection of members of a filter is in the
-- the filter. These are both `simp` lemmas.
#print Filter.sInter_mem
#print Filter.iInter_mem

/- We also have a smallest filter `âŠ¥` (the principal filter
generated by the empty set, also called the trivial filter)
and a biggest filter `âŠ¤` (generated by the universal set).-/

example (s : Set Î±) : s âˆˆ (âŠ¥ : Filter Î±) := Filter.mem_bot

example (s : Set Î±) : s âˆˆ (âŠ¤ : Filter Î±) â†” s = (âŠ¤ : Set Î±) := by
  simp only [Filter.mem_top, Set.top_eq_univ]

/- Some lemmas require filters to be nontrivial, so there is
a class `Filter.NeBot`, and these lemma take `F.NeBot` as an
instance parameter.-/

#print Filter.NeBot

-- Mathlib knows that a lot of filters are not trivial.
#synth Filter.NeBot (Filter.atTop (Î± := â„•))
#synth Filter.NeBot (nhds (1 : â„))

#print Filter.map_neBot
-- This is an instance, so mathlib automatically knows that,
-- if `F` is not trivial, then so is its `map` by any function.

/- If `F : Filter Î±` and `G : Filter Î²`, we can define the
product of `F` and `G`, which is a filter on `Î± Ã— Î²`. We
write this `Filter.prod F G` or `F Ã—Ë¢ G`.
For example, if `a,b : â„`, then `nhds a Ã—Ë¢ nhds b` will
be `nhds âŸ¨a, bâŸ©`, the filter of neighborhoods of `âŸ¨a, bâŸ©`
in `â„ Ã— â„`.-/

#print Filter.prod
-- F Ã—Ë¢ G = (Filter.comap Prod.fst F) âŠ“ (Filter.comap Prod.snd G)
-- This means that `F Ã—Ë¢ G` is the biggest filter `H` on
-- `Î± Ã— Î²` such that `Filter.map Prod.fst H â‰¤ F` and
-- `Filter.map Prod.snd H â‰¤ G`.

#print Filter.mem_prod_iff
-- s âˆˆ f Ã—Ë¢ g â†” âˆƒ tâ‚ âˆˆ f, âˆƒ tâ‚‚ âˆˆ g, tâ‚ Ã—Ë¢ tâ‚‚ âŠ† s

/- Actually, we also have arbitrary products of filters.-/
#print Filter.pi -- same formula as for `Filter.prod`:
-- â¨… i, Filter.comap (Function.eval i) (f i)

/- Filter bases:
This is a generalization of principal filters. If `F : Filter Î±`
and `s : Î¹ â†’ Set Î±` is a family of sets of `Î±`, then we say
that `s` is a basis of `F` if
`âˆ€ (t : Set Î±), t âˆˆ F â†” âˆƒ (i : Î¹), s i âŠ† t`.

This is expressed by a `Prop`-valued structure `Filter.HasBasis`.
-/

#print Filter.HasBasis
/-
/-- We say that a filter `l` has a basis `s : Î¹ â†’ Set Î±` bounded by `p : Î¹ â†’ Prop`,
if `t âˆˆ l` if and only if `t` includes `s i` for some `i` such that `p i`. -/
structure HasBasis (l : Filter Î±) (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set Î±) : Prop where
  /-- A set `t` belongs to a filter `l` iff it includes an element of the basis. -/
  mem_iff' : âˆ€ t : Set Î±, t âˆˆ l â†” âˆƒ i, p i âˆ§ s i âŠ† t

-/

-- Note the arguments: we don't just give `s : Î¹ â†’ Set Î±`, but
-- also a `p : Î¹ â†’ Prop` and only use as basis  the `s i`
-- for the `i : Î¹` such that `p i` holds. This is convenient
-- because `s` can then be defined on a more natural type, as in
-- the following example.
example (xâ‚€ : â„) : HasBasis (ğ“ xâ‚€) (fun Îµ : â„ â†¦ 0 < Îµ)
    fun Îµ â†¦ Ioo (xâ‚€ - Îµ) (xâ‚€ + Îµ) :=
  nhds_basis_Ioo_pos xâ‚€

example : HasBasis (atTop : Filter â„•) (fun _ : â„• â†¦ True) Ici := atTop_basis

-- Now to check convergence along filters, it suffices to
-- use the sets in the basis.
#check Filter.HasBasis.tendsto_iff  -- very useful result!

example (u : â„• â†’ â„) (xâ‚€ : â„) :
    Tendsto u atTop (ğ“ xâ‚€) â†”
    âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, u n âˆˆ Ioo (xâ‚€ - Îµ) (xâ‚€ + Îµ) := by
  have : atTop.HasBasis (fun _ : â„• â†¦ True) Ici := atTop_basis
  rw [this.tendsto_iff (nhds_basis_Ioo_pos xâ‚€)]
  simp

example (f : â„ â†’ â„) (a b : â„) :
    Tendsto f (ğ“ a) (ğ“ b) â†”
    âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x, x âˆˆ Ioo (a - Î´) (a + Î´) â†’ f x âˆˆ Ioo (b - Îµ) (b + Îµ) := by sorry

#check nhds_basis_opens

example (f : â„ â†’ â„) (a b : â„) :
    Tendsto f (ğ“ a) (ğ“ b) â†” âˆ€ (U : Set â„), IsOpen U âˆ§ b âˆˆ U â†’
    âˆƒ (V : Set â„), IsOpen V âˆ§ a âˆˆ V âˆ§ V âŠ† f â»Â¹' U := by sorry

-- If we know a basis of a filter, it is easy to describe
-- its members.
#check Filter.HasBasis.mem_iff

example (A : Set â„•) : A âˆˆ atTop â†” âˆƒ n, Set.Ici n âŠ† A := by
  rw [Filter.HasBasis.mem_iff (atTop_basis)]
  simp

/- Another use of filters is that they give a convenient
way to talk about properties that are true for `x` big enough,
for `x` close enough to a fixed point, for almost all `x` etc.

For this, we use the function `Filter.Eventually`. If
`p : Î± â†’ Prop` and `F : Filter Î±`, then `Filter.Eventually p F`
means that `{x | p x}` is an element of `F`. Intuitively, this
means that `p` is true on the "set" corresponding to `F`

The notation for this is:
`âˆ€á¶  x in F, p x`. (\ + forall + \ + ^f)
-/

example : âˆ€á¶  n in atTop (Î± := â„•), 2 â‰¤ n := sorry

example : âˆ€á¶  x in nhds (0 : â„), |x| â‰¤ 1/2 := sorry

/- Now let's see what the properties of a filter say about `âˆ€á¶ `:

(1) `âŠ¤ âˆˆ F` means that: `âˆ€ x, p x â†’ âˆ€á¶  x in F, p x`.-/
#check Eventually.of_forall

/-(2) The stability of `F` by taking a superset means that, if
`q : Î± â†’ Prop` is another function, and if
`âˆ€á¶  x, p x` and `âˆ€ x, p x â†’ q x`, then `âˆ€á¶  x, q x`.-/
#check Eventually.mono

/-(3) The stability of `F` by intersections means that, if
`âˆ€á¶  x in F, p x` and `âˆ€á¶  x in F, q x`, then
`âˆ€á¶  x in F, p x âˆ§ q x`.-/
#check Filter.Eventually.and

example (P Q : â„• â†’ Prop) (hP : âˆ€á¶  n in atTop, P n) (hQ : âˆ€á¶  n in atTop, Q n) :
    âˆ€á¶  n in atTop, P n âˆ§ Q n := sorry

/- There are two special cases of `Filter.Eventually` for equalities
and inequalities:-/
#print Filter.EventuallyEq
#print Filter.EventuallyLE


/- They have special notation too:-/
example (u v : â„• â†’ â„) : (âˆ€á¶  n in atTop, u n = v n) â†” u =á¶ [atTop] v := Iff.refl _

example (u v : â„• â†’ â„) : (âˆ€á¶  n in atTop, u n â‰¤ v n) â†” u â‰¤á¶ [atTop] v := Iff.refl _

-- For example, two sequences that are eventually equal
-- for the filter `atTop` have the same limit.
example (u v : â„• â†’ â„) (h : u =á¶ [atTop] v) (xâ‚€ : â„) :
    Tendsto u atTop (ğ“ xâ‚€) â†” Tendsto v atTop (ğ“ xâ‚€) :=
  tendsto_congr' h

/- There is a tactic called `filter_upwards` to deal with goals
of the `âˆ€á¶  s in F, ...`.-/

/-- From the documentation:
`filter_upwards [hâ‚, â‹¯, hâ‚™]` replaces a goal of the form `s âˆˆ f` and terms
`hâ‚ : tâ‚ âˆˆ f, â‹¯, hâ‚™ : tâ‚™ âˆˆ f` with `âˆ€ x, x âˆˆ tâ‚ â†’ â‹¯ â†’ x âˆˆ tâ‚™ â†’ x âˆˆ s`.
The list is an optional parameter, `[]` being its default value.

`filter_upwards [hâ‚, â‹¯, hâ‚™] with aâ‚ aâ‚‚ â‹¯ aâ‚–` is a short form for
`{ filter_upwards [hâ‚, â‹¯, hâ‚™], intros aâ‚ aâ‚‚ â‹¯ aâ‚– }`.

`filter_upwards [hâ‚, â‹¯, hâ‚™] using e` is a short form for
`{ filter_upwards [h1, â‹¯, hn], exact e }`.

Combining both shortcuts is done by writing
`filter_upwards [hâ‚, â‹¯, hâ‚™] with aâ‚ aâ‚‚ â‹¯ aâ‚– using e`.
Note that in this case, the `aáµ¢` terms can be used in `e`.
-/

-- Without `filter_upwards`.
example (P Q R : â„• â†’ Prop) (hP : âˆ€á¶  n in atTop, P n) (hQ : âˆ€á¶  n in atTop, Q n)
    (hR : âˆ€á¶  n in atTop, P n âˆ§ Q n â†’ R n) : âˆ€á¶  n in atTop, R n := by
  apply (hP.and (hQ.and hR)).mono
  rintro n âŸ¨h, h', h''âŸ©
  exact h'' âŸ¨h, h'âŸ©

example (P Q R : â„• â†’ Prop) (hP : âˆ€á¶  n in atTop, P n) (hQ : âˆ€á¶  n in atTop, Q n)
    (hR : âˆ€á¶  n in atTop, P n âˆ§ Q n â†’ R n) : âˆ€á¶  n in atTop, R n := by
  filter_upwards [hP, hQ, hR] with n h h' h''
  exact h'' âŸ¨h, h'âŸ©

-- An exercise: if the sequence `u` converges to `x` and
-- `u n` is in `M` for `n` big enough, then `x` is in the closure
-- of `M`.

example (u : â„• â†’ â„) (M : Set â„) (x : â„) (hux : Tendsto u atTop (ğ“ x))
    (huM : âˆ€á¶  n in atTop, u n âˆˆ M) : x âˆˆ closure M :=
  sorry

--Useful lemmas for the exercise:
#check mem_closure_iff_clusterPt
#print ClusterPt -- note that `ClusterPt F x` means by definition
                 -- that `ğ“ x âŠ“ F` is not the `âŠ¥` filter
#check le_principal_iff
#check neBot_of_le

/- Another filter notion is `Filter.Frequently`. You
would use it for example to express something like
"there exist arbitrarily large `n` in `â„•` such that so and so".-/

#print Filter.Frequently
-- `Filter.Frequently p F` means `Â¬âˆ€á¶  (x : Î±) in f, Â¬p x` i.e.
-- `{x | Â¬p x} âˆ‰ F`. It is written `âˆƒá¶  x in F, p x`.
-- This is actually equivalent to saying that, for every `A âˆˆ F`,
-- there exists `x âˆˆ A` such that `p x`. Don't believe me?

example (p : Î± â†’ Prop) (F : Filter Î±) :
    (âˆƒá¶  x in F, p x) â†” âˆ€ A âˆˆ F, âˆƒ x âˆˆ A, p x := by sorry
